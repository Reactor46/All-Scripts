<html>
<head>
<!--
*******************************************************************************
*  This HTA will prompt a user to select a new OU for their computer.         *
*  This will work so long as the user account making the change has the       *
*  required permissions on the domain.										  *
*  																			  *
*  If you want to start the OU enumeration at a different root level, edit    *
*  the 'sRoot' variable with an OU structure:                                 *
*   																		  *
*  ou=domain worktations,dc=contoso,dc=com                                    *
*                                                                             *
*******************************************************************************
-->
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1250">
  <title> Computer OU Move </title>
  <HTA:application 
  applicationName="MoveComputer" 
  border="thin" 
  version="o.1"
  maximizebutton="yes"
  showintaskbar="yes"
  singleinstance="yes"
  scroll="no" 
  sysmenu="yes"
  />
  <style type="text/css">
  BODY,html {
    background-color: rgb(0,0,0);
    color: white;
    border: thin white solid;
    margin: 10px 10px 10px 10px;
    font: 12px Verdana;  
  div {
    padding:10px;
   	font-family: "Trebuchet MS";
  }
  input {
	width:300px;
	border-style: solid; 
	border-width: 1px; 
	border-color: 999 ; 

 	filter:progid:DXImageTransform.Microsoft.Gradient
(GradientType=1, StartColorStr='white', EndColorStr='#E8E8FF');
  }
  #buttondiv {
	position:fixed
	bottom:0;
	width:100%;
}
  </style>
  <script language="vbscript">

  </script>
</head>
<body>
<div style="height:90%">
	<div style='padding:2px;font-weight:bold;font-size:90%'>
		<span style='width:120px;'>Computer name:</span>
		<input type="text" id='spnComputerName' onchange="changesmade('name')" name="computername"></input>
	</div>
	<div style='padding:2px;font-weight:bold;font-size:90%'>
		<span style='width:120px;'>Computer description:</span>
		<input type="text" id='spnDescription' onchange="changesmade('description')" name="computerdescription">
		</input>
	</div>
	<div style='padding:2px;font-weight:bold;font-size:90%'>
		<span style='width:120px;'>Current OU:</span>
		<span style='' id='spnCurrentOU'></span>
	</div>
	<div style='padding:2px;font-weight:bold;font-size:90%'>
		<span style='width:120px;'>Choose an OU:</span>
		<select style="width:300px" class='input' size="1" id="optOUs" TABINDEX="2" name="DomainList" onChange="OUChange(me.value)"></select>
		<br>
	</div>
	<div style='padding:2px;font-weight:bold;font-size:90%'>
		<span style='width:120px;'>Proposed OU:</span>
		<span id='spnStatus'></span>
	</div>
</div>
<div onmouseover="changesmade('other')" id='buttondiv' style='background-color:gray;padding:5px;font-weight:bold;min-height:1px;font-size:90%'>&nbsp;
	<button id='btnFakeSubmit' disabled=true type="button">Submit changes</button>
	<button style='display:none;' id='btnSubmit' readonly=true type="button" onclick="ConfirmMove(domainlist.value)">Submit changes</button>&nbsp;&nbsp;
	<button id='btnExit' style='' type="button" onclick='ExitApp("prompt")'>Exit</button>
</div>

<script language="javascript">
  //resize the HTA window
  varW = 800;
  varH = 240;
  window.resizeTo(varW,varH);
  varSW = screen.width;
  varSH = screen.height;
  window.moveTo((varSW/2)-(varW/2),(varSH/2)-(varH/2))

</script>

<script>
dim sRoot, sComputer, objADInfo, computer_OU, strComputerDN, strDescription
dim bChangesMade, bUpdateComputer, bUpdateDescription, bChangeOU, sProposedOU
dim bOUnotFound

bChangeOU = false
bChangesMade = false

'The LDAP root where we want to start our OU enumeration from.
' i.e.: ou=domain workstations,dc=domain,dc=local
' By default, the HTA will hit the root of your OU structure
' which includes user objects and other resource OUs

sRoot 			= ""

ImportDomains(sRoot)

Sub ImportDomains(sRoot)
	On Error Resume Next

	Set WshShell = CreateObject("WScript.Shell")
	Set WshSysEnv = WshShell.Environment("PROCESS")

	'Set registry key so the HTA can run without causing any weird ADO exceptions.
	WshShell.RegWrite "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\Zones\1\1406", 0, "REG_DWORD" 

	Set objADInfo 	= CreateObject("ADSystemInfo")
	Set objComputer = GetObject("LDAP://" & objADInfo.ComputerName)
	If sRoot = "" then 
		Set objRootDSE = GetObject("LDAP://rootDSE")
		strSchema = "LDAP://" & objRootDSE.Get("schemaNamingContext")
		'msgbox "ADsPath to schema: " & strSchema
		aSchema = split(replace(strSchema,"LDAP://",""),",")
		
		For i = 0 to ubound(aSchema)

			If instr(aSchema(i),"CN=") Then 
				'msgbox "processing: " & aSchema(i)
				strSchema = replace(strSchema,aSchema(i) & ",","")
			End If
		Next
		
		'msgbox "sRoot " & strSchema
		Set objDomain = GetObject(ucase(strSchema))
	Else
		If left(ucase(sRoot),7) <> "LDAP://" then
			sRoot = "LDAP://" & sRoot
		End If
		
		Set objDomain 	= GetObject(ucase(sRoot))
	End If
	
	'Get distinguished name of computer for use later.
	strComputerDN 	= objADInfo.ComputerName
	
	'Get plain-jane computer name
	sComputer 		= WshSysEnv("computername")
	computer_OU 	= replace(objComputer.Parent,"LDAP://","")
	
    'update a couple html elements with our new information.	
	document.getElementByID("spnComputerName").value 	= sComputer
	document.getElementByID("spnDescription").value		= GetDescription(sComputer)
	document.getElementbyID("spnCurrentOU").innerhtml 	= computer_OU
	
	'Populate our list with OU's from the root
	Call EnumOUs(objDomain, "")
	on error goto 0
	setOptionByValue document.getElementByID("optOUs"),computer_OU
	on error resume next

	objRecordSet.Close
	objConnection.Close

End Sub 'ImportDomains

Function ExitApp(var)
	If var = "force" then 
		sResult = 6
	Else
		'Prompt user to make sure they want to exit.
		sResult = Msgbox ("Are you sure you want to quit?",36,"Quit application?")
	End If

	'If response is 'yes', then close.
	If sResult = 6 then 
		window.close()
	End If
	
End Function 'ExitApp

Sub OUChange(sValue)
	sProposedOU = sValue
	if sProposedOU <> computer_OU and sProposedOU <> "" then bChangeOU = true

	'Update HTML element with values of our newly selected OU and
	' unhide our submit button to make the changes.
	document.getelementbyid("spnStatus").innerhtml 			= sValue

	bUpdateOU = true
End Sub 'OUChange

Sub ChangesMade(sVar)
	If document.getElementByID("spnComputerName").value <> sComputer then 
		bUpdateComputer 	= true
		bChangesMade 		= true
	else
		bUpdateComputer		= false
	End If
	If document.getElementByID("spnDescription").value <> GetDescription(sComputer) then 
		bUpdateDescription 	= true
		bChangesMade 		= true
	Else
		bUpdateDescription 	= false
	End If
	
	If sProposedOU <> computer_OU and sProposedOU <> "" then 
		bChangeOU 			= true
		bChangesMade 		= true
	Else
		bChangeOU			= false
	End If

	if bChangeOU = false and bUpdateDescription = false and bUpdateComputer = false then bChangesMade = false
	
	If bChangesMade = true then 
		document.getElementByID("btnSubmit").style.display 	= "inline"
		document.getelementbyid("btnFakeSubmit").style.display 	= "none"
	Else
		document.getElementByID("btnSubmit").style.display 	= "none"
		document.getelementbyid("btnFakeSubmit").style.display 	= "inline"
	End If
End Sub 'ChangesMade

Sub setOptionByValue(objSelect, myvalue)
	
	'If this computer is not in an OU, then don't generate an error.
	if  instr(lcase(myvalue),"cn=") then 

	Else
		for i = 0 to objSelect.options.length
		on error resume next
		if objSelect.options(i).value = myValue then
			objSelect.options(i).selected = true
			
			Exit for
		End if
	next 
	End If
	
	If err.number <> 0 then 
		bOUnotFound = true
		
		sResult = msgbox ("Could not preselect this computer's home OU structure.  " _
		& "This error often occurs if the computer was renamed without rebooting, " _ 
		& "the computer's home OU resides outside of the enumerated listing, or " _
		& "communication to the domain could not be established." & vbcrlf & vbcrlf _
		& "Would you like to reboot now?",36,"Error while enumerating OU")
		
		If sResult = 6 then 
		  On Error goto 0
			Set objWMIService = GetObject("winmgmts:{(Shutdown)}\\.\root\cimv2")
			Set colComputers = objWMIService.ExecQuery("Select * From Win32_OperatingSystem")
			For Each objSystem in colComputers
				objSystem.Reboot
			Next
		End If
		
		'document.getElementByID("spnComputerName").disabled = true
		'document.getElementByID("optOUs").disabled = true
		
	End If
End Sub 'setOptionByValue

Sub ConfirmMove(sValue)
	on error resume next
	Dim sMsg
	if bUpdateComputer = true then sMsg = "Changing computer name from '" & sComputer & "' to '" & trim(document.getElementByID("spnComputerName").value) & "'" & vbcrlf & vbcrlf
	if bUpdateDescription = true then sMsg = sMsg & "Changing computer description to '" & trim(document.getElementByID("spnDescription").value) & "'" & vbcrlf & vbcrlf
	'msgbox sProposedOU & vbcrlf & computer_OU
	if bChangeOU = true or bOUnotFound = true then sMsg = sMsg & "Moving computer to the following OU: " & vbcrlf & vbcrlf & sValue
	'Prompt user with yes/no messagebox to confirm move.
	sResult = Msgbox (sMsg,36,"Confirm actions?")
	
	'If response is 'yes', then make the OU move, prompt user with a message and then 
	' refresh the HTA.
	If sResult = 6 then

		'Try to write the new computer description to AD computer object attributes.
		Set objComputer = GetObject("LDAP://" & strComputerDN)
		objComputer.Put "Description" , trim(document.getElementByID("spnDescription").value)
		objComputer.SetInfo
		
		If err.number <> 0 then
			msgbox "There was an error while updating computer object's description to '" & trim(document.getElementByID("spnDescription").value) & "'",48,"Could not update AD computer description"
		End If
	
		If bChangeOU = true then 
			Set objNewOU = GetObject("LDAP://" & sValue)	
			Set objMoveComputer = objNewOU.MoveHere("LDAP://" & strComputerDN, "CN=" & sComputer)
			msgbox "OU move submitted.  Clicking OK will refresh this window.",64,"Request submitted"
			If err.number <> 0 then 
				msgbox "There was an error while trying to move this computer to the OU you " _
				& "selected.  Please check your permissions or contact your system " _
				& "administrator.",48,"An error occured."
			End If	
		End If
		

		
		SetDetails trim(document.getElementByID("spnComputerName").value),trim(document.getElementByID("spnDescription").value)
		Location.Reload(True)

	End If
End Sub 'ConfirmMOve

Sub EnumOUs(objParent, strOffset)
	On Error Resume Next
	' Recursive subroutine to enumerate OU's.
	objParent.Filter = Array("organizationalUnit")
	
	If Not objParent.OU="" Then
		Set objOption = Document.createElement("Option")
  		objOption.Text = strOffSet & objParent.OU
    	objOption.Value = objParent.Distinguishedname
    	DomainList.Add objOption
    End If

	For Each objChild In objParent
		'Call this recursive subroutine and add a blank string of 
		' characters to give the illusion of nested OU's in the 
		' listbox.
		Call EnumOUs(objChild, strOffset & "     ")
	Next
End Sub	'EnumOus

Function GetDescription(sComputer)
	Set objWMIService = GetObject("winmgmts:" & "{impersonationLevel=impersonate}!\\.\root\cimv2")
	Set colComputers = objWMIService.ExecQuery ("Select Description from Win32_OperatingSystem")

	For Each objComputer in colComputers
		strDescription = objComputer.Description
	Next
	
	GetDescription = strDescription
	
End Function 'GetComputerDetails

Sub SetDetails(strNewComputerName,strNewDescription)
	'msgbox strNewDescription
	Set objWMIService = GetObject("winmgmts:{impersonationLevel=impersonate}!\\.\root\cimv2")
	Set colComputers = objWMIService.ExecQuery ("Select Name from Win32_ComputerSystem")

	For Each objComputer in colComputers
		If objComputer.Name <> strNewComputerName Then
			'rename this computer
			intErrorCode 	= objComputer.Rename(strNewComputerName)

			//msgbox interrorcode
			If intErrorCode <> 0 then
				msgbox "There was an error while renaming this computer (" & sComputer & ") to '" & strNewComputerName & "'",48,"Could not rename computer"
			ElseIf intErrorCode = 5 then
				msgbox "You do not have sufficient permissions to rename this computer.  Please log in as an administrator and try again.",48,"Could not rename computer."
			ElseIf intErrorCode = 0 then
				bDontRefresh = true
				sResult = msgbox("Computer renamed successfully.  You must restart the computer as soon as possible to make changes effective on the domain.  Would you like to do so now?" & vbcrlf & vbcrlf & "Clicking 'No' will close the utility.",36,"Reboot?")
				
				If sResult = 6 then 
					On Error goto 0
					Set objWMIService = GetObject("winmgmts:{(Shutdown)}\\.\root\cimv2")
					Set colComputers = objWMIService.ExecQuery("Select * From Win32_OperatingSystem")
					For Each objSystem in colComputers
						objSystem.Reboot
					Next
				End If
				ExitApp("force")
			End If
		End If
	Next
	
	Set colComputers = objWMIService.ExecQuery ("Select * from Win32_OperatingSystem")
	For Each objComputer in colComputers
			'Update the description for this computer
			objComputer.Description = trim(document.getElementByID("spnDescription").value)
			intErrorCode = objComputer.Put_
	Next
	If intErrorCode <> 0 then
		msgbox "There was an error while updating the local description to '" & trim(document.getElementByID("spnDescription").value) & "'",48,"Could not update description"
	End If
End Sub 'SetDetails
</script>
</body>
</HTmL>
