<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<html xmlns="http://www.w3.org/1999/xhtml">
<head>
<title>Computer Info Tool</title>
<hta:application applicationname="Computer Info Tool"
 scroll="yes"
 singleinstance="no"
 icon="\\yourserver\yourshare\pcinfo.ico"
 windowstate="normal">
<style type="text/css">
body {
	margin:0px;
	background-color:#CBCBCB; /*#F6F6F6;*/
	font-family:Arial, Helvetica, sans-serif;
	font-size:14px;
	color:#595959;
}
h1 {
	font-size:24px;
	font-weight:bold;
	color:#FFFFFF;
	background-color:#2886C8;
	text-align:center;
	border-style:solid;
	border-width:thin;
	border-color:#C9E0F1;
	padding:5px;
}
h2 {
	font-size:18px;
	font-weight:bold;
}
h3 {
	font-size:16px;
	font-weight:bold;
}
h4 {
	font-size:12px;
	font-weight:bold;
}
a {
	color:#2886C8;
}

#Main {
	margin-left:20px;
	margin-right:20px;
}
#DisplayError {
	color:red;
	margin-left:20px;
	margin-right:20px;
}
#Footer {
	margin:15px;
	font-weight:bold;
	font-size:10px;
}
#Header {
	margin-left:160px;
	margin-right:20px;
	text-align:center;
}
#Tools {
	text-align:center;
	border-color:#595959;
	border-style:dotted;
	border-width:1px;
	background-color:#F6F6F6;
	margin-left:20px;
	margin-right:20px;
	margin-top:20px;
	padding:5px;
}
.Button { 
	color: #444444; 
} 
.InfoSectionHeader {
	font-size:20px;
	font-weight:bold;
	background-color:#595959;
	color:#FFFFFF;
	text-align:center;
	padding:5px;
	margin-top:0px;
	cursor:pointer;
}
.InfoSection {
	text-align:center;
	margin-bottom:10px;
	background-color:#FFFFFF;
	border-color:#595959;
	border-style:dotted;
	border-width:1px;
}
.InfoSectionBody {
	padding:10px;
}
.Link {
	text-decoration: underline;
	cursor:pointer;
	color:#2886C8;
}
.HeaderLink {
	text-decoration: underline;
	cursor:pointer;
	color:#FFFFFF;
}
.Table {
	/*width:90%;*/
    border: 2px solid;
	border-collapse: collapse;
	border-color: #696969;
}
.Table th {
	border: 1px dotted #111111;
	border-color: #787878;
	color: #FFFFFF;
	font: bold 12pt arial, sans-serif;
	background-color: #595959; /* #787878;*/
	text-align: left;
    padding=3px;
}
.Table td {
	border: 1px dotted #111111;
	border-color: #787878;
	font: bold 10pt arial, sans-serif;
	color: #787878;
 padding=5px;
}

</style>
<script language="VBScript">
	Option Explicit
	Const bytesToMB = 1024
    Const bytesToGB = 1073741824
    Const bytesToTB = 1099511627776
    Const adVarChar = 200
    Const adDate = 7
	Const MaxCharacters = 255
	Const adFldIsNullable = 32
	Const adInteger = 3
	Const adBigInt = 20
    Const blnConfirmKillProcess = true
	Const ADS_SECURE_AUTHENTICATION = 1
	Const wbemFlagReturnImmediately = &h10
    Const wbemFlagForwardOnly = &h20
	Const CONVERSION_FACTOR = 1048576
	Const CONVERSION_FACTOR1 = 1024
	Const ForAppending = 8  
	Const OverwriteExisting = True
	Const HKEY_USERS = &H80000003 
	Const REG_SZ = 1 
	Const REG_EXPAND_SZ = 2 
	Const REG_BINARY = 3 
	Const REG_DWORD = 4 
	Const REG_MULTI_SZ = 7 
    
    Private objWMIService
    Private strComputer
    private intProcessTimerID
	
	' ***************************************
	' Open Windows explorer to a given path
	' Used when clicking a link in the "Shares" section
	' ***************************************
	Sub OpenUNC(ByVal strPath)
		
		Dim objShell
			
		Set objShell = CreateObject("Wscript.Shell")
		strPath = "explorer.exe /e," & strPath
		objShell.Run strPath

	End Sub
	
	' ***************************************
	' Reboot computer
	' ***************************************
	Sub RebootComputer()
		Dim objItem, colItems
		strComputer = CurrentComputer.InnerHTML
		if MsgBox("Are you sure you want to reboot '" & strComputer & "'?",vbYesNo+vbExclamation,"Confirm Reboot") = vbYes then
			Set colItems = objWMIService.ExecQuery("Select * from Win32_OperatingSystem")
			For Each objItem in colItems
				objItem.Reboot()
			Next
			msgbox "Computer '" & strComputer & "' has been rebooted",vbOKOnly+vbInformation
		end if

	End Sub
	
	' ***************************************
	' Shutdown computer
	' ***************************************
	Sub ShutDownComputer()
		Dim objItem, colItems
		strComputer = CurrentComputer.InnerHTML
		
		Dim intOption
		intOption = InputBox("Shutdown computer '" & strComputer & "'" & vbcrlf & _
				"Options:" & vbcrlf & vbcrlf & _
				"0 = Log Off " & vbcrlf & _
				"4 = Forced Log Off" & vbcrlf & _
				"1 = Shut Down" & vbcrlf & _
				"5 = Forced Shutdown" & vbcrlf & _
				"2 = Reboot" & vbcrlf & _
				"6 = Forced Reboot" & vbcrlf & _
				"8 = Power Off" & vbcrlf & _
				"12 = Forced Power Off","Shutdown",1)
		SELECT CASE intOption
			CASE "0","4","1","5","2","6","8","12"
					Set colItems = objWMIService.ExecQuery("Select * from Win32_OperatingSystem")
					For Each objItem in colItems
						objItem.Win32Shutdown(intOption)
					Next
					msgbox "Command has been sent to '" & strComputer & "'",vbOKOnly+vbInformation
			CASE ""
			CASE Else
					msgbox "Invalid option specified:" & intOption,vbOKOnly+vbExclamation
		END SELECT

	End Sub
	
	' ***************************************
	' Kills the specified process
	' Called when "Kill Process" link is clicked in the Running Processes section
	' ***************************************
	Sub KillProcess(ByVal intProcessID, ByVal strName)
		Dim objItem, colItems
		strComputer = CurrentComputer.InnerHTML
		if blnConfirmKillProcess = True then
			if msgbox("Are you sure you want to kill the '" & strName & "' process on '" & strComputer & "'?",vbYesNo+vbExclamation,"Confirm Kill Process") = vbNo then
				exit sub
			end if
		end if
		'Set objWMIService = GetObject("winmgmts:\\" & strComputer & "\root\cimv2")
		Set colItems = objWMIService.ExecQuery("Select * from Win32_Process WHERE ProcessID = '" & _
											 intProcessID & "' AND Name = '" & strName & "'")

		For Each objItem In colItems
			objItem.Terminate()
		Next
		RefreshProcesses
	End Sub
	
	' ***************************************
	' Refreshes list of processes in the "Running Processes" section
	' ***************************************
	Sub RefreshProcesses
		strComputer = CurrentComputer.InnerHTML
		Set objWMIService = GetObject("winmgmts:\\" & strComputer & "\root\cimv2")
		ProcessesData.InnerHTML = RunningProcesses_HTML
	End Sub
	
	' ***************************************
	' Enables/Disables the auto refresh feature in the "Running Processes" section
	' ***************************************
	Sub SetProcessAutoRefresh
		dim intInterval
		' Get the refresh interval
		intInterval = ProcessAutoRefresh.Value
		' Remove the current auto-refresh
		window.clearInterval(intProcessTimerID)
		' Add an auto-refresh if specified
		if intInterval > 0 then
			intProcessTimerID = window.setInterval("RefreshProcesses", intInterval)
		end if
	End sub
	
	' ***************************************
	' Stores the sort value for "Running Processes" section in a hidden div
	' and refreshes the processes list with the new sort value
	' ***************************************
	Sub SortProcesses(byval strSort)
		' If sort link is clicked twice on the same column, sort descending
		if ProcessSort.InnerHTML = strSort then
			ProcessSort.InnerHTML = strSort & " DESC"
		else
			ProcessSort.InnerHTML = strSort
		end if
		RefreshProcesses()
	End Sub
	
	' ***************************************
	' Map as network drive on this computer to a share on another computer
	' Called from the "Shares" section. User is prompted for a drive letter
	' ***************************************
	Sub MapDrive(ByVal strPath)
		Dim objNetwork
		Dim strDrive
		strDrive = InputBox("Enter drive letter:","Drive Letter","Z")
		If strDrive <> "" Then
			strDrive = Left(strDrive,1) & ":"
		
			Set objNetwork = CreateObject("WScript.Network")
			objNetwork.MapNetworkDrive strDrive,strPath
		End If
	End Sub
	
	' ***************************************
	' Stores the sort value for "Running Processes" section in a hidden div
	' ***************************************
	Sub MapPrinter(ByVal strPath)
		Dim objNetwork
		Set objNetwork = createobject("Wscript.Network")
		objNetwork.AddWindowsPrinterConnection(strPath)
		If MsgBox("Make Printer Default?",vbYesNo+vbQuestion,"Default Printer") = vbYes Then
			objNetwork.SetDefaultPrinter strPath
		End If
	End Sub
	
	' ***************************************
	' Replace special HTML characters
	' ***************************************
	Function HTMLEncode(strValue)
		HTMLEncode= Replace(Replace(strValue,"<","&lt;"),">","&gt;")
	End Function
	
	' ***************************************
	' Replace with Regular Expression  http://www.addedbytes.com/lab/vbscript-regular-expressions/ (currently not in use)
	' Function replaces pattern with replacement
    ' varIgnoreCase must be TRUE (match is case insensitive) or FALSE (match is case sensitive)
	' ***************************************
	Function Ereg_Replace(strOriginalString, strPattern, strReplacement, varIgnoreCase)
		Dim objRegExp : set objRegExp = new RegExp
		With objRegExp
			.Pattern = strPattern
			.IgnoreCase = varIgnoreCase
			.Global = True
		End with
		Ereg_Replace = objRegExp.replace(strOriginalString, strReplacement)
		Set objRegExp = nothing
	End Function
	
	' ***************************************
	' Convert Bytes to MB,GB or TB as appropriate
	' ***************************************
    Function ConvertToDiskUnit(ByVal value) 
        IF (value/bytesToTb) > 1 Then
            ConvertToDiskUnit = round(value / bytesToTB,1) & " TB"
        ELSEIF (value/bytesToGb) > 1 Then
            ConvertToDiskUnit = round(value / bytesToGB,1) & " GB"
        Else
            ConvertToDiskUnit = round(value / bytesToMB,1) & " MB"
        END If
    End Function
    
	' ***************************************
	' Convert Bytes to MB, GB or TB for Memory
	' ***************************************
    Function ConvertMemorySize(ByVal value) 
        IF (value/bytesToTb) > 1 Then
            ConvertMemorySize = round(value / bytesToTB,1) & " TB"
        ELSEIF (value/bytesToGb) > 1 Then
            ConvertMemorySize = round(value / bytesToGB,1) & " GB"
        Else
            ConvertMemorySize = round(value / bytesToMB,1) & " MB"
        END If
    End Function
	
	' ***************************************
	' Convert integer value to string
	' ***************************************
    Function GetMemoryType(ByVal intType)
       	Dim strType
    	Select case intType
    	Case 0
 			strType = "Unknown"
 		Case 1
 			strType = "Other"
 		Case 2
 			strType = "DRAM"
 		Case 3
 			strType = "Synchronous DRAM"
		Case 4
			strType = "Cache DRAM"
		Case 5
			strType = "EDO" 
		Case 6
		 	strType = "EDRAM" 
		Case 7
		 	strType = "VRAM"
		Case 8
		 	strType = "SRAM"
		Case 9
		  	strType = "RAM"
		Case 10
		 	strType = "ROM"
		Case 11
			strType = " Flash"
		Case 12
			strType = "EEPROM"
		Case 13
 			strType = "FEPROM"
		Case 14
		 	strType = " EPROM"
		Case 15
		 	strType = " CDRAM"
		Case 16
		  	strType = "3DRAM"
		Case 17
		 	strType = " SDRAM"
		Case 18
		 	strType = " SGRAM"
		Case 19
		 	strType = " RDRAM"
		Case 20
		 	strType = " DDR"
		Case 21
		 	strType = " DDR-2"
		Case Else
		 	strType = "Unknown"
		End Select
		GetMemoryType=strType
    End Function
    
    ' ***************************************
	' Convert Integer value to string
	' ***************************************
    Function GetMemoryFormFactor(ByVal intFormFactor)
    	Dim strFormFactor
    	Select Case intFormFactor
    	Case 0
			strFormFactor = "Unknown"
		Case 1
			strFormFactor = "Other"
		Case 2
			strFormFactor = "SIP"
		Case 3
			strFormFactor = "DIP"
		Case 4
			strFormFactor = "ZIP"
		Case 5
			strFormFactor = "SOJ"
		Case 6
			strFormFactor = "Proprietary"
		Case 7
			strFormFactor = "SIMM"
		Case 8
			strFormFactor = "DIMM"
		Case 9
			strFormFactor = "TSOP"
		Case 10
			strFormFactor = "PGA"
		Case 11
			strFormFactor = "RIMM"
		Case 12
			strFormFactor = "SODIMM"
		Case 13
			strFormFactor = "SRIMM"
		Case 14
			strFormFactor = "SMD"
		Case 15
			strFormFactor = "SSMP"
		Case 16
			strFormFactor = "QFP"
		Case 17
			strFormFactor = "TQFP"
		Case 18
			strFormFactor = "SOIC"
		Case 19
			strFormFactor = "LCC"
		Case 20
			strFormFactor = "PLCC"
		Case 21
			strFormFactor = "BGA"
		Case 22
			strFormFactor = "FPBGA"
		Case 23
			strFormFactor = "LGA"
		case Else
			strFormFactor = "Unknown"
		End Select
		GetMemoryFormFactor=strFormFactor
    End Function
    
   	' ***************************************
	' Convert date string to a more readable format
	' ***************************************
    Function FormatDate(ByVal strValue)
    	Dim strDate
    	If ISNULL(strValue) Or strValue = "" Then
    		strDate = ""
    	Else
    		strDate = Mid(strValue,5,2) & "-" & MID(strValue,7,2) & "-" & Left(strValue,4) & " " & _
    				Mid(strValue,9,2) & ":" &  Mid(strValue,11,2)
    	End If
    	FormatDate = strDate
    End Function
 
	' ***************************************
	' Convert date string to date
	' ***************************************
	Function WMIDateStringToDate(dtmBootup)
    WMIDateStringToDate =  _
        CDate(Mid(dtmBootup, 5, 2) & "/" & _
        Mid(dtmBootup, 7, 2) & "/" & Left(dtmBootup, 4) _
        & " " & Mid (dtmBootup, 9, 2) & ":" & _
        Mid(dtmBootup, 11, 2) & ":" & Mid(dtmBootup, _
        13, 2))
	End Function
    
	' ***************************************
	' Clear existing report data
	' ***************************************
	Sub Reset
		ProcessAutoRefresh.Value="0"
		window.clearInterval(intProcessTimerID)
		Main.Style.Display = "none"
		Tools.Style.Display = "none"
		LogicalDisk.InnerHTML=""
		PhysicalDisk.InnerHTML=""
		Processor.InnerHTML =""
		Memory.InnerHTML = ""
		OS.InnerHTML =""
		Shares.InnerHTML=""
		Printers.InnerHTML=""
		MappedDrives.InnerHTML=""
		DisplayError.InnerHTML=""
	End Sub
	
	' ***************************************
	' Main procedure used to generate report
	' Calls other procedures that generate the HTML for each section
	' ***************************************
	Sub GenerateReport
		Reset()
		strComputer = txtComputer.Value
		If strComputer = "" Then 
			Dim objNetwork
			set objNetwork = createobject("wscript.network")
			strComputer =  objNetwork.ComputerName
			txtComputer.Value = strComputer
		End If
		CurrentComputer.InnerHTML = strComputer
		Dim objSWbemLocator
		On Error Resume Next
		If txtUserName.Value <> "" Then
			Set objSWbemLocator = CreateObject("WbemScripting.SWbemLocator")
			Set objWMIService = objSWbemLocator.ConnectServer _
				(strComputer, "root\cimv2", txtUserName.Value, txtPassword.Value)
			objWMIService.Security_.ImpersonationLevel = 3
		Else
			Set objWMIService = GetObject("winmgmts:\\" & strComputer & "\root\cimv2")
		End If
		
		If Err.Number <> 0 Then
			On Error GoTo 0
			DisplayError.InnerHTML = "Error connecting to '" & strComputer & "'"
			Err.Clear
			On Error GoTo 0
		Exit Sub
		
		End If
		On Error GoTo 0
		Main.Style.Display = ""
		Tools.Style.Display = ""
		
		LogicalDisk.InnerHTML = LogicalDisk_HTML
		PhysicalDisk.InnerHTML = PhysicalDisk_HTML
		Processor.InnerHTML = Processor_HTML
		Memory.InnerHTML = Memory_HTML
		OS.InnerHTML = OS_HTML
		Shares.InnerHTML = Shares_HTML
		Printers.InnerHTML = Printers_HTML
		MappedDrives.InnerHTML = MappedDrives_HTML
		Users.InnerHTML = Users_HTML
		'ProcessesData.InnerHTML = RunningProcesses_HTML


	End sub

	' ********************************
	' Creates output to Excel report
	' ********************************
	Sub OutputToExcel

	Dim strHTAPath, strReportPath, objFSO, objFile, objShell, strOutputFile
	If Mid(document.location, 6, 3) = "///" Then
    	strHTAPath = Mid(Replace(Replace(document.location, "%20", " "), "/", "\"), 9)
    Else
		strHTAPath = Mid(Replace(Replace(document.location, "%20", " "), "/", "\"), 6)
    End If
    
	'UNC or local path to export Excel reports
	'strReportPath = "\\yourserver\yourshare\yourfolder"
	strReportPath = Left(strHTAPath, InStrRev(strHTAPath, "\") - 1) & "\Reports"
    Set objFSO = CreateObject("Scripting.FileSystemObject")
    If objFSO.FolderExists(strReportPath) = False Then objFSO.CreateFolder strReportPath
	strOutputFile = strReportPath & "\" & CurrentComputer.InnerHTML & "_ComputerInfo.xls"
	Set objFSO = CreateObject("Scripting.FileSystemObject")
	Set objFile = objFSO.CreateTextFile(strOutputFile, True)
	objFile.WriteLine "Computer: " & CurrentComputer.InnerHTML & "  -  " & Date()
	objFile.WriteLine OS.InnerHTML
	objFile.WriteLine LogicalDisk.InnerHTML
	objFile.WriteLine PhysicalDisk.InnerHTML
	objFile.WriteLine Processor.InnerHTML
	objFile.WriteLine Memory.InnerHTML
	objFile.WriteLine Shares.InnerHTML
	objFile.WriteLine Printers.InnerHTML
	objFile.WriteLine MappedDrives.InnerHTML
	objFile.Close
		
	Set objShell = CreateObject("WScript.Shell")

	objShell.Run "excel """ & strOutputFile & """", 1, False

	End Sub
	
	' ***************************************
	' Gets a list of running processes and returns HTML for the "Running Processes" section
	' ***************************************
	Function RunningProcesses_HTML
		Dim row,strHTML,strFilter
		Dim DataList,colItems,objItem,strUser,strDomain
		Dim strWMIQuery
		strFilter = txtProcessFilter.Value
		
		' Recordset is used to sort data from WMI
		Set DataList = CreateObject("ADOR.Recordset")
		DataList.Fields.Append "Name", adVarChar, MaxCharacters, adFldIsNullable
		DataList.Fields.Append "WorkingSet", adInteger, adFldIsNullable
		DataList.Fields.Append "CreationDate",adVarChar, MaxCharacters, adFldIsNullable
		DataList.Fields.Append "Description",adVarChar,MaxCharacters, adFldIsNullable
		DataList.Fields.Append "ProcessID",adInteger, adFldIsNullable
		DataList.Fields.Append "CPUTime",adInteger, adFldIsNullable
		DataList.Fields.Append "Caption",adVarChar,MaxCharacters, adFldIsNullable
		DataList.Fields.Append "Owner",adVarChar,MaxCharacters, adFldIsNullable
		DataList.Fields.Append "Path",adVarChar,MaxCharacters, adFldIsNullable
		DataList.Open

		strWMIQuery = "Select * From Win32_Process"
		' Add filter if required
		if strFilter <> "" then
			strWMIQuery = strWMIQuery & " WHERE Name LIKE '%" & strFilter & "%'"
		end if
		
		Set colItems = objWMIService.ExecQuery(strWMIQuery)

		' Load WMI data into recordset
		For Each objItem in colItems
			Dim strOwner
    		DataList.AddNew
    		on error resume next
    		DataList("Name") = HTMLEncode(objItem.Name)
    		DataList("WorkingSet") = objItem.WorkingSetSize
    		DataList("CreationDate") = objItem.CreationDate
    		DataList("Description") = HTMLEncode(objItem.Description)
    		DataList("ProcessID") = objItem.ProcessID
    		DataList("Caption") = objItem.Caption
    		DataList("CPUTime") = (CSng(objItem.KernelModeTime) + CSng(objItem.UserModeTime)) / 10000000
    		If objItem.GetOwner (strUser, strDomain) = 0 Then
				strOwner = strDomain & "\" & strUser
			End If
			DataList("Owner") = strOwner
			on error goto 0
    		DataList.Update
		Next
		' Sort recordset
		DataList.Sort = ProcessSort.InnerHTML
		' Check if recordset is not empty
		If DataList.BOF = FALSE then
			DataList.MoveFirst
			' Generate HTML table report with running processes
			strHTML = strHTML & "<table class=""Table"">"
			strHTML = strHTML &  "<tr><th><span onclick=""SortProcesses('Name')"" class=""HeaderLink"">Name</span></th>" & _
						"<th><span onclick=""SortProcesses('CreationDate')"" class=""HeaderLink"">Creation Date</span></th>" & _
						"<th><span onclick=""SortProcesses('Owner')"" class=""HeaderLink"">Owner</span></th>" & _
						"<th><span onclick=""SortProcesses('WorkingSet')"" class=""HeaderLink"">Working Set</span></th>" & _
						"<th><span onclick=""SortProcesses('CPUTime')"" class=""HeaderLink"">Total CPU Time(s)</span></th>" & _
						"<th>&nbsp;</th></tr>"
						
			Do Until DataList.EOF
				Dim strCaption,strCreationDate,strPath,intProcessID,strDescription
				Dim strWorkingSet
								
				strHTML = strHTML & "<tr>" & _
									"<td><div title=""" & DataList("Path") & """>" & DataList("Name") & "</div></td>" & _
									"<td>" & FormatDate(DataList("CreationDate")) & "</td>" & _
									"<td>" & DataList("Owner") & "</td>" & _
									"<td>" & ConvertToDiskUnit(DataList("WorkingSet")) & "</td>" & _
									"<td>" & DataList("CPUTime")  & "</td>" & _
									"<td><span onclick=""KillProcess '" & DataList("ProcessID") & "','" & DataList("Name") & "'"" class=""Link"">Kill Process</span></td>" & _
									"</tr>"
				DataList.MoveNext
			Loop
			strHTML = strHTML & "</table>"
		end if
		DataList.Close
		strHTML = strHTML & "<br>Last Refresh:" & Now()
		
		RunningProcesses_HTML=strHTML
	End Function
	
	' ***************************************
	' Get Remote User
	' *************************************** 
	Function GetRemoteUser 'retrieves ID of logged-on user of remote PC
	  Dim arrName,objWMIService,colItems,objItem,strShortUserID
	  Set objWMIService = GetObject("winmgmts:{ImpersonationLevel=impersonate}\\" & strComputer & "\root\cimv2")
	  Set colItems = objWMIService.ExecQuery("Select * FROM Win32_ComputerSystem")
	  For Each objItem In colItems
		If IsNull(trim(objItem.UserName)) Then
		  GetRemoteUser = "None"
		Else
		  arrName = Split(objItem.UserName,"\")
		  strShortUserID = arrName(1) 'extract ID from results
		  GetRemoteUser = strShortUserID
		End If
	  Next
	End Function

	' ***************************************
	' Gets Domain SID from User 
	' *************************************** 
	Function GetSIDFromUser(UserName) 

		Dim DomainName, Result, WMIUser 
			If InStr(UserName, "\") > 0 Then 
				DomainName = Mid(UserName, 1, InStr(UserName, "\") - 1) 
				UserName = Mid(UserName, InStr(UserName, "\") + 1) 
			Else 
				DomainName = CreateObject("WScript.Network").UserDomain 
			End If 
		On Error Resume Next 
		
		Set WMIUser = GetObject("winmgmts:{impersonationlevel=impersonate}!" _ 
			& "/root/cimv2:Win32_UserAccount.Domain='" & DomainName & "'" _ 
			& ",Name='" & UserName & "'") 
			If Err = 0 Then Result = WMIUser.SID Else Result = "" 
			On Error GoTo 0 
			GetSIDFromUser = Result 
	End Function 

	' ***************************************
	' Returns a list of shared folders and shared printers and
	' returns HTML for the "Shares" section
	' *************************************** 
    Function Shares_HTML
    	Dim objItem, colItems
		Dim strHTML
		
		' Query to return shared folders
		Set colItems = objWMIService.ExecQuery("Select * from Win32_Share WHERE Type = 2147483648 OR Type = 0")

		strHTML = strHTML & "<table><tr><td style=""vertical-align:top"">"
		strHTML = strHTML & "<table class=""Table""><tr><th colspan=""2"">Folder Shares</th></tr>"
		For Each objItem In colItems
			Dim strShare 
			strShare = HTMLEncode(objItem.Name)
			strHTML = strHTML & "<tr><td><span class=""Link"" onclick=""OpenUNC('\\" & strComputer & "\" & strShare & "')"">" & strShare & "</span></td>"
			strHTML = strHTML &	"<td><input class=""Button"" type=""button"" onclick=""MapDrive('\\" & strComputer & "\" & strShare & "')"" value=""Map Drive""></input></td></tr>"
		Next
		strHTML = strHTML & "</table></td>"
		
		' Query to return shared printers
		Set colItems = objWMIService.ExecQuery("Select * from Win32_Share WHERE Type = 2147483649 OR Type = 1")
		
		strHTML = strHTML & "<td style=""vertical-align:top"">"
		strHTML = strHTML & "<table class=""Table""><tr><th colspan=""2"">Printer Shares</th></tr>"
		For Each objItem In colItems
			Dim strPrinterShare 
			strPrinterShare = HTMLEncode(objItem.Name)
			strHTML = strHTML & "<tr><td><span class=""Link"" onclick=""OpenUNC('\\" & strComputer & "\" & strPrinterShare & "')"">" & strPrinterShare & "</span></td>"
			strHTML = strHTML &	"<td><input class=""Button"" type=""button"" onclick=""MapPrinter('\\" & strComputer & "\" & strPrinterShare & "')"" value=""Connect Printer""></input></td></tr>"
		Next
		strHTML = strHTML & "</table></td></table>"

		Shares_HTML = strHTML 
    End Function
    
	' ***************************************
	' Returns a list of installed printers
	' *************************************** 
	Function Printers_HTML
		
		Dim objItem,colItems,arrRegKeys,objRegistry,arrUserName,sUser,objWbem,subkey,GetPrinters
		Dim strPrinters,lngRtn,strKey,objSID,prtpath2enumerate,arrPrtNames,intCount,regpath
		Dim strHTML

		Set colItems =  objWMIService.ExecQuery ("Select * from Win32_Printer")

		For Each objItem in colItems
		On Error Resume Next 
		If objItem.Network = True Then 
			strPrinter = HMLEncode(objItem.Printer)
		For Each field In document.PrintersList
			if objItem.Name = field.value Then field.checked = "True"
		Next
		End If
		On Error GoTo 0
	
	Next

		strHTML = strHTML & "<table><tr><td style=""vertical-align:top"">"
		strHTML = strHTML & "<table class=""Table"">"
		For Each objItem In colItems
			Dim strPrinter 
			strPrinter = HTMLEncode(objItem.Name)
			strHTML = strHTML & "<tr><td><span>" & strPrinter & "</span></td>"
		Next

		sUser=GetRemoteUser() 
		
		If sUser <> "None" Then
			Set objRegistry = GetObject("winmgmts://" & strComputer & "/root/default:StdRegProv")	 
			Set objWbem = GetObject("winmgmts:")
			lngRtn = objRegistry.EnumKey(HKEY_USERS, "", arrRegKeys)	
						
			For Each strKey In arrRegKeys
				If (UCase(strKey) <> ".DEFAULT") And (UCase(Right(strKey, 8)) <> "_CLASSES") Then
					Set objSID = objWbem.Get("Win32_SID.SID='" & strKey & "'")
					
					If ucase(objSID.accountname) = ucase(sUser) Then 
					  prtpath2enumerate = strKey & "\Printers\Connections" 'strkey is the SID
					  objRegistry.enumkey HKEY_USERS, prtpath2enumerate, arrPrtNames
					  
						intCount = 0 
						If Not (IsNull(arrPrtNames)) Then
							For Each subkey In arrPrtNames
							regpath = strkey & "\Printers\Connections"  
								objRegistry.getstringvalue hkey_users, regpath, subkey
								intCount = intCount + 1 'increment 
								strPrinters = strPrinters & Replace(subkey,",","\") & "<br />"
								GetPrinters = strPrinters
								'strHTML = strHTML & "<tr><td><span>" & GetPrinters & "</span></td>"
							Next
							strHTML = strHTML & "<tr><td><span>" & GetPrinters & "</span></td>"
								Else
							GetPrinters = "Cannot determine printers"	
						End If
					End If
						'GetPrinters = "Cannot determine printers"
				End If
				'strHTML = strHTML & "<tr><td><span>" & GetPrinters & "</span></td>"
			Next
				Else
			GetPrinters = "Cannot determine printers"
		End If
		
		strHTML = strHTML & "</table></td>"	
		
		Printers_HTML = strHTML 
    End Function
	
	' ***************************************
	' Gets a list of mapped drives
	' *************************************** 
	Function MappedDrives_HTML
		Dim sUser,objRegistry,arrRegKeys,lngRtn,strKey,objSID,objWbem,regpath2enumerate,arrkeynames,regpath,regentry,subkey,dapath
		Dim strHTML
		
		sUser=GetRemoteUser() 
		
		If sUser <> "None" Then
			Set objRegistry = GetObject("winmgmts://" & strComputer & "/root/default:StdRegProv")	 
			Set objWbem = GetObject("winmgmts:")
			lngRtn = objRegistry.EnumKey(HKEY_USERS, "", arrRegKeys)	
			
		strHTML = strHTML & "<table><tr><td style=""vertical-align:top"">"
		strHTML = strHTML & "<table class=""Table"">"
						
			For Each strKey In arrRegKeys
				If (UCase(strKey) <> ".DEFAULT") And (UCase(Right(strKey, 8)) <> "_CLASSES") Then
				Set objSID = objWbem.Get("Win32_SID.SID='" & strKey & "'")
					
					If ucase(objSID.accountname) = ucase(sUser) Then 
					  regpath2enumerate = strKey & "\Network" 'strkey is the SID
					  objRegistry.enumkey HKEY_USERS, regpath2enumerate, arrkeynames
							
						If Not (IsEmpty(arrkeynames)) Then 
							On Error Resume Next
							For Each subkey In arrkeynames 
							'On Error Resume Next
								regpath = strkey & "\Network\" & subkey 
								regentry = "RemotePath" 
								objRegistry.getstringvalue hkey_users, regpath, regentry, dapath 
								strHTML = strHTML & "<tr><td><span>" & subkey & ":" & vbTab & "<a href='" & dapath & "'>" & dapath & "</a>" & "</span></td>"
							Next 
						End If 
					End If 
				End If 
			Next 
		End If
	
		strHTML = strHTML & "</table></td>"
				
		MappedDrives_HTML = strHTML
	End Function
		
    ' ***************************************
	' Gets computer system info to be included in the "OS / General" section
	' *************************************** 
    Sub GetComputerSystemInfo(BYRef strDNSHostName,ByRef strDomain,ByRef strDomainRole, _
    						ByRef strManufacturer,ByRef strModel,ByRef strUserName, _
							ByRef strLogonServer)
    	Dim objItem, colItems
		
		Set colItems = objWMIService.ExecQuery("Select * from Win32_ComputerSystem")
		For Each objItem In colItems
			On Error Resume Next 
			strDNSHostName = HTMLEncode(objItem.DNSHostName)
			strDomain = HTMLEncode(objItem.Domain)
			strManufacturer = HTMLEncode(objItem.Manufacturer)
			strModel = HTMLEncode(objItem.Model)
			strUserName = HTMLEncode(objItem.UserName)
			strLogonServer = HTMLEncode(objItem.LogonServer)
			On Error GoTo 0
			If strUserName = "" Then
				strUserName = "{not logged in}"
			End If
			Select Case objItem.DomainRole
				Case 0
				strDomainRole="Standalone Workstation"
				Case 1
				strDomainRole="Member Workstation"
				Case 2
				strDomainRole="Standalone Server"
				Case 3
				strDomainRole="Member Server"
				Case 4
				strDomainRole="Backup Domain Controller"
				Case 5
				strDomainRole="Primary Domain Controller"
				Case Else
				strDomainRole = "Unknown (" & strDomainRole & ")"
			End Select
		Next
    End Sub
	  
    ' ***************************************
	' Gets computer BIOS info to be included in the "OS / General" section
	' *************************************** 
	Sub GetBIOSInfo(BYRef strSerialNumber,BYRef strBIOSVersion,BYRef strBIOSReleaseDate)
    	Dim objItem, colItems
		
		Set colItems = objWMIService.ExecQuery("Select * from Win32_BIOS")
		For Each objItem In colItems
			On Error Resume Next 
			strSerialNumber = HTMLEncode(objItem.SerialNumber)
			strBIOSVersion = HTMLEncode(objItem.SMBIOSBIOSVersion)
			strBIOSReleaseDate = WMIDateStringToDate(objItem.ReleaseDate)
			On Error GoTo 0
		Next
    End Sub
	
	' ***************************************
	' Gets computer Asset tag to be included in the "OS / General" section
	' *************************************** 
	Sub GetAssetTag(BYRef strSMBIOSAssetTag)
    	Dim objItem, colItems
		
		Set colItems = objWMIService.ExecQuery("Select * from Win32_SystemEnclosure")
		For Each objItem In colItems
			On Error Resume Next 
			strSMBIOSAssetTag = HTMLEncode(objItem.SMBIOSAssetTag)
			On Error GoTo 0
		Next
    End Sub

	' ***************************************
	' Gets IP and MAC addresses to be included in the "OS / General" section
	' *************************************** 
	Sub GetNICInfo(BYRef strIPAddress,strMACAddress,strIPAddress1)
    	Dim objItem, colItems, IPArray(), intSize
    	intSize = 0
			
		Set colItems = objWMIService.ExecQuery("Select * from Win32_NetworkAdapterConfiguration WHERE IPEnabled = True")
		For Each objItem In colItems
			On Error Resume Next 
			strMACAddress = HTMLEncode(objItem.MACAddress)
			If Not IsNull(objItem.IPAddress) Then
				strIPAddress = Join(objItem.IPAddress)
				If objItem.DHCPEnabled = "True" Then
				strIPAddress = strIPAddress & " (DHCP)"
								Else
									strIPAddress = strIPAddress & " (Static)"
							End If
			Else
				strIPAddress = ""
			End If			
			On Error GoTo 0
		ReDim Preserve IPArray(intSize)
		IPArray(intSize) = strIPAddress
		intSize = intSize + 1	
			
		Next
		strIPaddress1 = Join (IPArray, ", ")
		
    End Sub
	
	' ***************************************
	' Gets CPU name to be included in the "OS / General" section
	' *************************************** 
	Sub GetCPUInfo(BYRef strCPUName)
    	Dim objItem, colItems
		
		Set colItems = objWMIService.ExecQuery("Select * from Win32_Processor")
		For Each objItem In colItems
			On Error Resume Next 
			strCPUName = HTMLEncode(objItem.Name)
			On Error GoTo 0
		Next
    End Sub
	
	On Error Resume Next
	
	' ***************************************
	' Gets OS Architecture model to be included in the "OS / General" section
	' *************************************** 
	Sub GetOSArchitecture(BYRef strOSArchitecture)
    	Dim objItem, colItems
		
		Set colItems = objWMIService.ExecQuery("SELECT * FROM Win32_Processor", "WQL", _
                                          wbemFlagReturnImmediately + wbemFlagForwardOnly)
		For Each objItem In colItems
			On Error Resume Next 
			strOSArchitecture = HTMLEncode(objItem.AddressWidth)
			On Error GoTo 0
		Next
    End Sub
	
	On Error Resume Next
	
	' ***************************************
	' Gets Default Printer to be included in the "OS / General" section
	' *************************************** 
	Sub GetDefaultPrinter(BYRef strDefaultPrinter)
		Dim arrRegKeys,objRegistry,sUser,objWbem,subkey,lngRtn,strKey,objSID,regpath,regentry,arrkeynames,dapath,regpath2enumerate
		
		sUser=GetRemoteUser() 
		
		If sUser <> "None" Then
			Set objRegistry = GetObject("winmgmts://" & strComputer & "/root/default:StdRegProv")	 
			Set objWbem = GetObject("winmgmts:")
			lngRtn = objRegistry.EnumKey(HKEY_USERS, "", arrRegKeys)	
						
			For Each strKey In arrRegKeys
				If (UCase(strKey) <> ".DEFAULT") And (UCase(Right(strKey, 8)) <> "_CLASSES") Then
					Set objSID = objWbem.Get("Win32_SID.SID='" & strKey & "'")
					
					If ucase(objSID.accountname) = ucase(sUser) Then 
						regpath2enumerate  = strKey & "\Software\Microsoft\Windows NT\CurrentVersion\Windows" 'strkey is the SID
						objRegistry.enumkey HKEY_USERS, regpath2enumerate, arrkeynames

						If Not (IsEmpty(arrkeynames)) Then 
							On Error Resume Next
							For Each subkey In arrkeynames 
								regpath= strkey & "\Software\Microsoft\Windows NT\CurrentVersion\Windows" & subkey 
								regentry = "Device" 
								objRegistry.getstringvalue hkey_users, regpath, regentry, dapath 
									If (IsEmpty(regentry)) Then 
										regpath= strkey & "\Software\Microsoft\Windows NT\CurrentVersion\Windows"
									End If
								objRegistry.getstringvalue hkey_users, regpath, regentry, dapath 
								strDefaultPrinter = Replace(Replace(Replace(dapath,",winspool"," "),"Ne", "NetPort "),":"," ")
							Next 
						End If
					End If
				End If
			Next
		End If
		
	End Sub
	
	On Error Resume Next
		
    ' ***************************************
	' Returns a HTML report for the "Operating System/General" section
	' ***************************************  
    Function OS_HTML()
    
    	Dim objItem, colItems
		Dim strHTML
		Dim strDNSHostName,strDomain,strDomainRole,strManufacturer,strModel,strUserName,strLogonServer
		Dim strIPAddress,strMACAddress,strIPAddress1
		Dim strSerialNumber,strBIOSVersion,strBIOSReleaseDate
		Dim strSMBIOSAssetTag
		Dim strCPUName,strOSArchitecture
		Dim strDefaultPrinter
		Dim strLogonSession
		GetComputerSystemInfo strDNSHostName,strDomain,strDomainRole,strManufacturer,strModel,strUserName,strLogonServer
		GetBIOSInfo strSerialNumber,strBIOSVersion,strBIOSReleaseDate
		GetAssetTag strSMBIOSAssetTag
		GetNICInfo strIPAddress,strMACAddress,strIPAddress1
		GetCPUInfo strCPUName
		GetOSArchitecture strOSArchitecture
		GetDefaultPrinter strDefaultPrinter
		GetLastLogonDate strLogonSession
		
		Set colItems = objWMIService.ExecQuery("Select * from Win32_OperatingSystem")
		
		strHTML = "<h3>Current User: " & strUserName & "</h3>"
		strHTML = strHTML & "<h4>Last Run: " & Now() & "</h4>"
		
		For Each objItem In colItems
			Dim strComputerRole
			Dim strCaption,strBuildNumber,strInstallDate,dtmBootup,dtmLastBootUpTime,dtmSystemUptime
			Dim intServicePackMajor,intServicePackMinor,intTotalVisibleMemorySize
			Dim intFreePhysicalMemory,intTotalVirtualMemorySize,intFreeVirtualMemory
			On Error Resume Next
			strCaption = HTMLEncode(objItem.Caption)
			strBuildNumber = HTMLEncode(objItem.BuildNumber)
			intServicePackMajor = objItem.ServicePackMajorVersion
			intServicePackMinor = objItem.ServicePackMinorVersion
			intTotalVisibleMemorySize = ConvertMemorySize(objItem.TotalVisibleMemorySize)
			intFreePhysicalMemory = ConvertMemorySize(objItem.FreePhysicalMemory)
			intFreeVirtualMemory =  ConvertMemorySize(objItem.FreeVirtualMemory)
			intTotalVirtualMemorySize = ConvertMemorySize(objItem.TotalVirtualMemorySize)
			strInstallDate = WMIDateStringToDate(objItem.InstallDate)
			dtmBootup = (objItem.LastBootUpTime)
			dtmLastBootUpTime = WMIDateStringToDate(dtmBootup)
			dtmSystemUptime = DateDiff("d", dtmLastBootUpTime, Now)
			On Error GoTo 0
						
			strHTML = strHTML & "<table class=""Table"">" & _
					 	"<tr>" & _
						"<th>Manufacturer:</th><td>" & strManufacturer & "</td>" & _
					 	"<th>Model:</th><td>" & strModel & "</td>" & _
						"<tr>" & _
						"<th>Service Tag:</th><td>" & strSerialNumber & "</td>" & _
						"<th>Asset Tag:</th><td>" & strSMBIOSAssetTag & "</td>" & _
						"<tr>" & _
					 	"<th>Operating System:</th><td>" & strCaption & "</td>" & _
					 	"<th>Build Number:</th><td>" & strBuildNumber & "</td>" & _
						"</tr><tr>" & _
					 	"<th>Service Pack:</th><td>" & intServicePackMajor & "." &  intServicePackMinor & "</td>" & _
						"<th>BIOS Version:</th><td>" & strBIOSVersion & "  released " & strBIOSReleaseDate & "</td>" & _
						"</tr><tr>" & _
						"<th>CPU:</th><td>" & strCPUName & "</td>" & _
					 	"<th>Architecture:</th><td>" & strOSArchitecture & "-bit </td>" & _
					 	"</tr><tr>" & _
						"<th>IP Address:</th><td>" & strIPAddress1 & "</td>" & _
					 	"<th>MAC Address:</th><td>" & strMACAddress & "</td>" & _
						"</tr><tr>" & _
					 	"<th>DNS Host Name:</th><td>" & strDNSHostName & "</td>" & _
					 	"<th>Domain:</th><td>" & strDomain & "</td>" & _
					 	"</tr><tr>" & _
					 	"<th>Total Physical Memory:</th><td>" & intTotalVisibleMemorySize & "</td>" & _
					 	"<th>Free Physical Memory:</th><td>" & intFreePhysicalMemory  & "</td>" & _
					 	"</tr><tr>" & _
					 	"<th>Total Virtual Memory:</th><td>" & intTotalVirtualMemorySize & "</td>" & _
					 	"<th>Free Virtual Memory:</th><td>" & intFreeVirtualMemory & "</td>" & _
						"</tr><tr>" & _
						"<th>Install Date:</th><td>" & strInstallDate & "</td>" & _
					 	"<th>Last Reboot:</th><td>" & dtmLastBootUpTime & "</td>" & _
						"</tr><tr>" & _
					 	"<th>Reserved:</th><td>" & " " & "</td>" & _
					 	"<th>System Uptime:</th><td>" & dtmSystemUptime & " days </td>" & _
						"</tr><tr>" & _
						"<th>Default Printer:</th><td>" & strDefaultPrinter & "</td>" & _
					 	"<th>Last Logon:</th><td>" & strLogonSession & "</td>" & _
						"</tr>" & _
					 	"</table>"
						
			Exit For
		Next
		
		OS_HTML = strHTML
		
    End Function
    
	' ***************************************
	' Get the number of memory slots and memory arrays
	' for the memory section
	' ***************************************
    Function GetMemoryArrayInfo(ByRef intSlots,ByRef intArrays)
    	Dim objItem, colItems
		intSlots = 0
		intArrays = 0
    	Set colItems = objWMIService.ExecQuery("Select * from Win32_PhysicalMemoryArray WHERE Use=3")
    	
    	For Each objItem In colItems
    		intSlots = intSlots + objItem.MemoryDevices
    		intArrays = intArrays + 1
    	Next
    End Function
 
	' ***************************************
	' Gets Last Logon Date and Time
	' *************************************** 
	Sub GetLastLogonDate(BYRef strLogonSession)
		Dim objSession, colSession
	
		Set colSession = objWMIService.ExecQuery _
				("Select * from Win32_LogonSession where AuthenticationPackage = 'NTLM'")

		For Each objSession in colSession
			strLogonSession = WMIDateStringToDate(objSession.StartTime)
		Next
	End Sub
		
	On Error Resume Next	
		
	' ***************************************
	' Returns a HTML report for the "Memory" section
	' ***************************************  
	Function Memory_HTML()
    	Dim objItem, colItems
		Dim strHTML
		
		Set colItems = objWMIService.ExecQuery("Select * from Win32_PhysicalMemory")

		strHTML = "<table class=""Table"">" & _
				"<tr>" & _
				"<th>BankLabel</th>" & _
				"<th>Capacity</th>" & _
				"<th>Caption</th>" & _
				"<th>Description</th>" & _
				"<th>DeviceLocator </th>" & _
				"<th>Manufacturer</th>" & _
				"<th>Memory Type</th>" & _
				"<th>Form Factor</th>" & _
				"<th>Model</th>" & _
				"<th>Speed</th>" & _
				"</tr>"

		For Each objItem In colItems
			Dim strBankLabel,strCaption,strDescription,strDeviceLocator
			Dim strManufacturer,strMemoryType,strFormFactor,strModel
			Dim strCapacity,intSpeed
			On Error Resume Next
			strBankLabel = HTMLEncode(objItem.BankLabel) 
			strCapacity = ConvertToDiskUnit(objItem.Capacity)
			strCaption = HTMLEncode(objItem.Caption)
			strDescription = HTMLEncode(objItem.Description)
			strDeviceLocator = HTMLEncode(objItem.DeviceLocator)
			strManufacturer = HTMLEncode(objItem.Manufacturer)
			strMemoryType = GetMemoryType(objItem.MemoryType)
			strFormFactor = GetMemoryFormFactor(objItem.FormFactor)
			strModel = HTMLEncode(objItem.Model)
			intSpeed = objItem.Speed
			On Error GoTo 0
		
			strHTML = strHTML & "<tr><td>" & strBankLabel & "</td>" & _
								"<td>" & strCapacity & "</td>" & _
								"<td>" & strCaption & "</td>" & _
								"<td>" & strDescription & "</td>" & _
								"<td>" & strDeviceLocator & "</td>" & _
								"<td>" & strManufacturer & "</td>" & _
								"<td>" & strMemoryType & "</td>" & _
								"<td>" & strFormFactor & "</td>" & _
								"<td>" & strModel & "</td>" & _
								"<td>" & intSpeed & "</td>" & _
								"</tr>"
		Next
		strHTML = strHTML & "</table>"
		
		Dim intSlots,intArrays
		GetMemoryArrayInfo intSlots,intArrays
		
		strHTML = strHTML & "Total Memory Slots:" & intSlots & ", Memory Arrays:" & intArrays
		
		Memory_HTML=strHTML
    End Function
    
     ' ***************************************
	' Returns a HTML report for the "Processor" section
	' ***************************************  
    Function Processor_HTML()
    	Dim objItem, colItems
		Dim strHTML
		
		Set colItems = objWMIService.ExecQuery("Select * from Win32_Processor")

		strHTML = "<table class=""Table"">" & _
				"<tr>" & _
				"<th>Name</th>" & _
				"<th>Manufacturer</th>" & _
				"<th>Description</th>" & _
				"<th>Address Width</th>" & _
				"<th>Current Clock Speed</th>" & _
				"<th>Data Width</th>" & _
				"<th>Device ID</th>" & _
				"<th>Ext Clock</th>" & _
				"<th>L2 Cache</th>" & _
				"<th>Max Clock Speed</th>" & _
				"<th>#Cores</th>" & _
				"<th>#Logical Processors</th>" & _
				"</tr>"

		For Each objItem in colItems
			Dim strName,strManufacturer,strDescription,strDeviceID
			Dim intAddressWidth,intCurrentClockSpeed,intDataWidth,intExtClock
			Dim intL2CacheSize,intMaxClockSpeed, intNumberOfCores,intNumberOfLogicalProcessors
			On Error Resume Next
			strName = HTMLEncode(objItem.Name)
			strManufacturer = HTMLEncode(objItem.Manufacturer)
			strDescription = HTMLEncode(objItem.Description)
			intAddressWidth = objItem.AddressWidth
			intCurrentClockSpeed = objItem.CurrentClockSpeed
			intDataWidth = objItem.DataWidth
			strDeviceID = HTMLEncode(objItem.DeviceID)
			intExtClock = objItem.ExtClock
			intL2CacheSize = objItem.L2CacheSize
			intMaxClockSpeed = objItem.MaxClockSpeed
			intNumberOfCores  = objItem.NumberOfCores
			intNumberOfLogicalProcessors= objItem.NumberOfLogicalProcessors
			On Error GoTo 0
			strHTML = strHTML & "<tr><td>" & strName & "</td>" & _
								"<td>" & strManufacturer & "</td>" & _
								"<td>" & strDescription & "</td>" & _
								"<td>" & intAddressWidth & "</td>" & _
								"<td>" & intCurrentClockSpeed & "</td>" & _
								"<td>" & intDataWidth & "</td>" & _
								"<td>" & strDeviceID  & "</td>" & _
								"<td>" & intExtClock & "</td>" & _
								"<td>" & intL2CacheSize & "</td>" & _
								"<td>" & intMaxClockSpeed & "</td>" & _
								"<td>" & intNumberOfCores & "</td>" & _
								"<td>" & intNumberOfLogicalProcessors & "</td>" & _
								"</tr>"
		Next
		strHTML = strHTML & "</table>"
		
		Processor_HTML=strHTML
    End Function
    
    ' ***************************************
	' Returns a HTML report for the "Physical Disk" section
	' ***************************************  
    Function PhysicalDisk_HTML()
    	Dim objItem, colItems
		Dim strHTML
		
		Set colItems = objWMIService.ExecQuery("Select * from Win32_DiskDrive")

		strHTML = "<table class=""Table""><tr><th>Caption</th><th>Manufacturer</th>" & _
				"<th>Model</th><th>Size</th><th>Serial</th><th>Media Type</th><th>#Partitions</th><th>DeviceID</th><th>Firmware</th><th>Interface</th></tr>"

		For Each objItem in colItems
			Dim intSize,intPartitions
			Dim strSize,strCaption,strManufacturer,strModel,strMediaType
			Dim strDeviceID,strFirmwareRevision,strInterfaceType, strSerialNumber
			intSize = objItem.Size
			If IsNumeric(intSize) = False Then
				intSize = 0
			End If
			On Error Resume Next
			strCaption= HTMLEncode(objItem.Caption) 
			strSize = ConvertToDiskUnit(intSize)
			strSerialNumber = HTMLEncode(objItem.SerialNumber)
			strMediaType = HTMLEncode(objItem.MediaType)
			intPartitions = HTMLEncode(objItem.Partitions)
			strDeviceID = HTMLEncode(objItem.DeviceID)
			strFirmwareRevision = HTMLEncode(objItem.FirmwareRevision)
			strInterfaceType = HTMLEncode(objItem.InterfaceType)
			strModel = HTMLEncode(objItem.Model)
			strManufacturer = HTMLEncode(objItem.Manufacturer)
			On Error GoTo 0
		
			strHTML = strHTML & "<tr><td>" & strCaption & "</td>" & _
								"<td>" & strManufacturer & "</td>" & _
								"<td>" & strModel & "</td>" & _
								"<td>" & strSize & "</td>" & _
								"<td>" & strSerialNumber & "</td>" & _
								"<td>" & strMediaType & "</td>" & _
								"<td>" & intPartitions & "</td>" & _
								"<td>" & strDeviceID & "</td>" & _
								"<td>" & strFirmwareRevision & "</td>" & _
								"<td>" & strInterfaceType & "</td>" & _
								"</tr>"
		Next
		strHTML = strHTML & "</table>"
		
		PhysicalDisk_HTML=strHTML
    	
    End Function

	' ***************************************
	' Returns a HTML report for the "Logical Disk" section
	' ***************************************  
	Function LogicalDisk_HTML()

		Dim objItem, colItems
		Dim strDriveType, strDiskSize, strHTML
		
		Set colItems = objWMIService.ExecQuery("Select * from Win32_LogicalDisk WHERE DriveType = 3")

		strHTML = "<table class=""Table""><tr><th>Drive</th><th>Name</th><th>File System</th><th>Size</th><th>Used</th><th>Free</th><th>Free(%)</th></tr>"

		For Each objItem in colItems
	
			Dim pctFreeSpace,strFreeSpace,strusedSpace,strName,strFileSystem,strVolumeName
			Dim intFreeSpace, intSize
			On Error Resume Next
			intFreeSpace = objItem.FreeSpace
			intSize = objItem.Size
			If ISNUMERIC(intFreeSpace) = False Then
				intFreeSpace=0
			End If
			If IsNumeric(intSize) = False Then
				intSize = 0
			End If
			If objItem.FreeSpace > 0 Then
				pctFreeSpace = round(((intFreeSpace / intSize) * 100),0)
			Else
				pctFreeSpace=0
			End If
			strDiskSize = ConvertToDiskUnit(intSize) 
			strFreeSpace = ConvertToDiskUnit(intFreeSpace)
			strUsedSpace = ConvertToDiskUnit(intSize-intFreeSpace)
			strName = HTMLEncode(objItem.Name)
			strVolumeName = HTMLEncode(objItem.VolumeName)
			strFileSystem = HTMLEncode(objItem.FileSystem)
			On Error GoTo 0

			dim strChart 
			strChart = "<div width=100%;""><span style=""padding:0px;margin:0px;width=" & 100-pctFreeSpace & _
				"%;background-color:blue;"">&nbsp;</span><span style=""padding:0px;margin:0px;width=" & pctFreeSpace & _
				"%;background-color:#FF00FF;"">&nbsp;</span></div>"

			strHTML = strHTML & "<tr><td>" & strName & "</td><td>" & _
					strVolumeName & "</td><td>" & strFileSystem & "</td><td>" & _
					strDiskSize & "</td><td>" & strUsedSpace & "</td><td>" & _
					strFreeSpace & "</td><td>" &  pctFreeSpace & "%</td></tr>" & _
					"<tr><td colspan=""7"">" & strChart & "</td></tr>"
		Next
		strHTML = strHTML + "</table></br>"

		LogicalDisk_HTML = strHTML
	End Function
	
	' ***************************************
	' Returns a HTML report for the "Users" section
	' ***************************************  
	Function Users_HTML
		Dim strHTML,colItems,objItem, i, strAccountControl
		Set colItems = objWMIService.ExecQuery("Select * from Win32_UserAccount WHERE LocalAccount=True")
		strHTML = "<table class=""Table""><tr><th>Account Name</th><th>Full Name</th><th>Description</th><th></th><th></th></tr>"
		i = 1
		For Each objItem In colItems
			if objItem.Disabled then
				strAccountControl = "<span id=""AccountControl" & i & """ class=""Link"" OnClick=""ToggleEnableDisable '" & objItem.Name & "','" & strComputer & "','AccountControl" & i & "'"">Enable</span>"
			else
				strAccountControl = "<span id=""AccountControl" & i & """ class=""Link"" OnClick=""ToggleEnableDisable '" & objItem.Name & "','" & strComputer & "','AccountControl" & i & "'"">Disable</span>"
			end if
			strHTML = strHTML & "<tr><td>" & objItem.Name & "</td><td>" & objItem.FullName & "</td><td>" & objItem.Description & _
				"</td><td><input id=""PWD" & i & """ type=""password""></input><span onclick=""ResetPassword '" & objItem.Name & "','" & _
				strComputer & "','PWD" & i & "'"" class=""Link"">Reset Password</span></td><td>" & strAccountControl & "</td></tr>"
			i = i + 1
		next
		strHTML = strHTML & "</table>"
		Users_HTML = strHTML
	End Function
	
	' ***************************************
	' Disables/Enables a user account
	' ***************************************  
	Sub ToggleEnableDisable(strUserName,strComputerName,strID)
		Dim objUser, objNT
		If txtUserName.Value <> "" then
			Set objNT = GetObject("WinNT:")
			Set objUser = objNT.OpenDSObject("WinNT://" & strComputerName & "/" & strUserName & ", user", txtUserName.Value, txtPassword.Value, ADS_SECURE_AUTHENTICATION)
		Else
			Set objUser = GetObject("WinNT://" & strComputerName & "/" & strUserName & ", user")
		End If
		If objUser.AccountDisabled = True Then
			objUser.AccountDisabled = False
			objUser.SetInfo
			document.getElementById(strID).InnerHTML="Disable"
		Else
			objUser.AccountDisabled = True
			objUser.SetInfo
			document.getElementById(strID).InnerHTML="Enable"
		End If
		set objUser = nothing
		set objNT = nothing
	End Sub
	
	' ***************************************
	' Resets a users password
	' ***************************************  
	Sub ResetPassword(strUserName,strComputerName,strPasswordID)
		Dim strPassword, objUser, objNT
		strPassword = document.getElementById(strPasswordID).Value
		If txtUserName.Value <> "" then
			Set objNT = GetObject("WinNT:")
			Set objUser = objNT.OpenDSObject("WinNT://" & strComputerName & "/" & strUserName & ", user", txtUserName.Value, txtPassword.Value, ADS_SECURE_AUTHENTICATION)
		Else
			Set objUser = GetObject("WinNT://" & strComputerName & "/" & strUserName & ", user")
		End If
		objUser.SetPassword strPassword
		set objUser = nothing 
		set objNT = nothing
		msgbox "Password Changed",vbOKOnly+vbInformation,"Computer Info"
	End Sub
	
	' ***************************************
	' Opens Computer Management Console
	' ***************************************  
	Sub ManageComputer
		dim objSh
		set objSh = createobject("wscript.shell")  
		objSh.run "compmgmt.msc /computer:" & strComputer
		set objSh = nothing
	End Sub
	
	' ***************************************
	' Opens Services Management Console
	' ***************************************  
	Sub ServicesMgmt
		dim objSh
		set objSh = createobject("wscript.shell")  
		objSh.run "services.msc /computer:" & strComputer
		set objSh = nothing
	End Sub
	
	' ***************************************
	' Opens Event Viewer
	' ***************************************  
	Sub EventViewer
		dim objSh
		set objSh = createobject("wscript.shell")  
		objSh.run "eventvwr.exe " & strComputer
		set objSh = nothing
	End Sub
	
	' ***************************************
	' Opens Shared Folders
	' ***************************************  
	Sub SharedFolders
		dim objSh
		set objSh = createobject("wscript.shell")  
		objSh.run "fsmgmt.msc /computer:" & strComputer
		set objSh = nothing
	End Sub

	' ***************************************
	' Opens a Remote Desktop Connection
	' ***************************************  
	Sub ConnectRDP
		dim objSh
		set objSh = createobject("wscript.shell")  
		objSh.run "mstsc.exe /v:" & strComputer
		set objSh = nothing
	End Sub
	
	' ***************************************
	' Opens an Ultra VNC Connection
	' ***************************************  
	Sub ConnectVNC
		dim objSh
		set objSh = createobject("wscript.shell")  
		objSh.run "vncviewer.exe " & strComputer
		set objSh = nothing
	End Sub
	
	' ***************************************
	' Send a Ping Request
	' ***************************************  
	Sub PingRequest
		dim objSh
		set objSh = createobject("wscript.shell")  
		objSh.run "ping -t " & strComputer
		set objSh = nothing
	End Sub
		
	' ***************************************
	' Opens a Remote Command Prompt
	' ***************************************  
	Sub RemoteCmd
		dim objSh
		set objSh = createobject("wscript.shell")  
		objSh.run "psexec " & "\\" & strComputer & " cmd.exe"
		set objSh = nothing
	End Sub
	
</script>
<script type="text/javascript">
	/* Toggle expand/collapse state for specified section */
	function toggleDisplay(obj) {
		var el = document.getElementById(obj);
		if ( el.style.display != 'none' ) {
			el.style.display = 'none';
		}
		else {
			el.style.display = '';
		}
	}
	/* Toggle expand/collapse state for all sections */
	function toggleAll() {
		var el = document.getElementById("OS");
		var display = ''
		if ( el.style.display != 'none' ) {
			display = 'none';
		}
		el.style.display = display;
		el = document.getElementById("Memory");
		el.style.display = display;
		el = document.getElementById("LogicalDisk");
		el.style.display = display;
		el = document.getElementById("PhysicalDisk");
		el.style.display = display;
		el = document.getElementById("Processor");
		el.style.display = display;
		el = document.getElementById("Shares");
		el.style.display = display;
		el = document.getElementById("Printers");
		el.style.display = display;
		el = document.getElementById("MappedDrives");
		el.style.display = display;
		el = document.getElementById("Processes");
		el.style.display = display;
		el = document.getElementById("Users");
		el.style.display = display;
	}

</script>
</head>
<body>
<h1>Computer Information Tool</h1>
<div id="Header" style="padding-bottom:0px;margin-bottom:0px;">
	<table style="font-weight:bold;"><tr><td>UserName:</td><td><input id="txtUserName"></input></td><td style="font-size:8px;">(Optional)</td><tr>
	<tr><td>Password:</td><td><input type="password" id="txtPassword"></input></td><td style="font-size:8px;">(Optional)</td></tr>
	<tr><td>Computer:</td><td><input id="txtComputer"></input></td>&nbsp;&nbsp;
	<td><input class="Button" style="font-size:10px;" onClick="GenerateReport()" type="submit" value="Generate"></td>
  </td></tr>
  </table>
 </div>
 <div id="Tools" style="display:none;margin-bottom:0px;">
   <span class="Link" onClick="RebootComputer">Reboot</span>&nbsp;|&nbsp;
   <span class="Link" onClick="ShutDownComputer">Shutdown</span>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
   <span class="Link" onClick="ManageComputer">Manage</span>&nbsp;|&nbsp;
   <span class="Link" onClick="ServicesMgmt">Services</span>&nbsp;|&nbsp;
   <span class="Link" onClick="EventViewer">Events</span>&nbsp;|&nbsp;
   <span class="Link" onClick="SharedFolders">Shares</span>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
   <span class="Link" onClick="PingRequest">Ping</span>&nbsp;|&nbsp;
   <span class="Link" onClick="RemoteCmd">RCmd</span>&nbsp;|&nbsp;
   <span class="Link" onClick="ConnectRDP">RDP</span>&nbsp;|&nbsp;
   <span class="Link" onClick="ConnectVNC">VNC</span>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
   <input class="Button" style="font-size:10px;" onClick="OutputToExcel()" type="button" value="Output to Excel"></input>
   </div>
<div id="Main" style="display:none;">
  <div id="CurrentComputer" style="display:none"></div>
  <div style="text-align:right;">
  <span class="Link" style="font-weight:bold;" onclick="javascript:toggleAll();">Expand/Collapse All</span>
  </div>
  <div class="InfoSection">
    <div onClick="javascript:toggleDisplay('OS');"  class="InfoSectionHeader">Operating System / General</div>
    <div id="OS" class="InfoSectionBody" ></div>
  </div>
  <div class="InfoSection">
    <div onClick="javascript:toggleDisplay('LogicalDisk');"  class="InfoSectionHeader">Logical Disk</div>
    <div id="LogicalDisk" class="InfoSectionBody"></div>
  </div>
  <div class="InfoSection">
    <div onClick="javascript:toggleDisplay('PhysicalDisk');" class="InfoSectionHeader">Physical Disk</div>
    <div id="PhysicalDisk" class="InfoSectionBody"></div>
  </div>
  <div class="InfoSection">
    <div onClick="javascript:toggleDisplay('Memory');"  class="InfoSectionHeader">Memory</div>
    <div id="Memory" class="InfoSectionBody"></div>
  </div>
  <div class="InfoSection">
    <div onClick="javascript:toggleDisplay('Processor');"  class="InfoSectionHeader">Processor</div>
    <div id="Processor" class="InfoSectionBody"></div>
  </div>
  <div class="InfoSection">
    <div onClick="javascript:toggleDisplay('Shares');"  class="InfoSectionHeader">Shares</div>
    <div id="Shares" class="InfoSectionBody"></div>
  </div>
  <div class="InfoSection">
    <div onClick="javascript:toggleDisplay('Printers');"  class="InfoSectionHeader">Printers</div>
    <div id="Printers" class="InfoSectionBody"></div>
  </div>
  <div class="InfoSection">
    <div onClick="javascript:toggleDisplay('MappedDrives');"  class="InfoSectionHeader">Mapped Drives</div>
    <div id="MappedDrives" class="InfoSectionBody"></div>
  </div>
    <div class="InfoSection">
    <div onClick="javascript:toggleDisplay('Users');"  class="InfoSectionHeader">Users</div>
    <div id="Users" class="InfoSectionBody"></div>
  </div>
  <div class="InfoSection">
    <div onClick="javascript:toggleDisplay('Processes');"  class="InfoSectionHeader">Running Processes</div>
    <div id="Processes" class="InfoSectionBody">
    	Auto Refresh Interval:<select id="ProcessAutoRefresh" onchange="SetProcessAutoRefresh">
		  <option value ="0">None</option>
		  <option value ="1000">1 second</option>
		  <option value ="2000">2 seconds</option>
		  <option value ="3000">3 seconds</option>
		  <option value ="5000">5 seconds</option>
		  <option value ="10000">10 seconds</option>
		  <option value ="20000">20 seconds</option>
		  <option value ="30000">30 seconds</option>
		  <option value ="60000">1 minute</option>
		</select>
		Filter (Optional):<input id="txtProcessFilter"></input>
		<br/><br/>
		<span onclick="RefreshProcesses()" style=""font-weight:bold"" class="Link">Refresh Processes</span>
		<br/><br/>
		
   		<div id="ProcessesData"></div>
    	</div>
    	<div id="ProcessSort" style="display:none">Name</div>
  </div>
</div>
<div id="DisplayError"></div>
<div id="Footer">
  <div style="float:left;">Your Support Name<br /> Your Company Name</div>
  <div style="float:right;">Computer Info Tool<br />&nbsp;Admin version 1.0</div>
</div>
</body>
</html>