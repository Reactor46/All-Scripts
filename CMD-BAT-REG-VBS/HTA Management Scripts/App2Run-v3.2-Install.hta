<HTML>
<Head>
<Title>Add Application to Run v3.2</Title>
<HTA:APPLICATION 
     ID="Apps2RunHTA"
     APPLICATIONNAME="Add Application to Run v3.0"
     SINGLEINSTANCE="yes"
>
</Head>
<SCRIPT LANGUAGE="VBScript">
'=*=*=*=*=*=*=*=*=*=*=*=
' Install-Apps2Run.HTA
' Coded By Assaf Miron 
' Date : 30/12/07
' V1.2 Date : 31/12/07
' V2 Date : 01/01/08
' V3 Date : 04/02/09
' thanks to Dima Donhin and Shoval Cohen
'=*=*=*=*=*=*=*=*=*=*=*=
'########################
' This HTA is a Single installation File that allows you to add Friendly Aliases
' to all of your favorite Applications, MSC Files, Scripts and Folders (can be used over Remote Folders to).
' Apps2Run adds a Link to the Selected Files Right Click Menu so you can easly Add these Application a Friendly Alias and Open them from "Run".
'########################

'########################
' Difine some Consts
'########################
Const HKEY_LOCAL_MACHINE = &H80000002
Const VerNum = "3.2"
Set objShell = CreateObject("WScript.Shell")
Set objFSO = CreateObject("Scripting.FileSystemObject")
Set TypeLib = CreateObject("Scriptlet.TypeLib")

Dim AppCount
Dim GUID
Dim intLeft
Dim intTop

Sub Window_onBeforeUnLoad
    intLeft = window.screenLeft
    intTop = window.screenTop

    Const ForWriting = 2
    Set objFSO = CreateObject("Scripting.FileSystemObject")
    Set objFile = objFSO.OpenTextFile("HTA.ini", ForWriting,True)
    objFile.WriteLine intLeft & "," & intTop
    objFile.Close
End Sub

Sub CreateShortCuts(instPath)
'########################
' This Sub Creates the Shortcuts in the Start menu
' it uses the system32 Shell32 Icons
'########################
Const ALL_USERS_PROGRAMS = &H17&
Const SYSTEM32 = &H25&
Set objShellApp = CreateObject("Shell.Application")
Set AllUsersFolder = objShellApp.Namespace(ALL_USERS_PROGRAMS)
Set AllUsersFolderItem = AllUsersFolder.Self
Set SystemFolder = objShellApp.Namespace(SYSTEM32)
Set SystemFolderItem = SystemFolder.Self
If objFSO.FolderExists(AllUsersFolderItem.Path & "\Apps2Run") Then
    Set objFol = objFSO.GetFolder(AllUsersFolderItem.Path & "\Apps2Run")
Else
	Set objFol = objFSO.CreateFolder(AllUsersFolderItem.Path & "\Apps2Run")
End If
Set objShort = objShell.CreateShortcut(objFol.Path & "\Apps2Run.lnk")
objShort.TargetPath = instPath & "\Apps2Run.HTA"
objShort.IconLocation = SystemFolderItem.Path & "\Shell32.dll,76"
objShort.Save
Set objShort = Nothing
Set objShort = objShell.CreateShortcut(objFol.Path & "\UnInstall Apps2Run.lnk")
objShort.TargetPath = instPath & "\Uninstall-Apps2Run.vbs"
objShort.IconLocation = SystemFolderItem.Path & "\Shell32.dll,172"
objShort.Save
End Sub

Sub BrowseFolder()
'########################
' This sub opens the Breowse folder Window
' and sets the chosen destination to the HTA for installation
'########################
On Error Resume Next
Const WINDOW_HANDLE = 0
Const NO_OPTIONS = 0

Set objShellApp = CreateObject("Shell.Application")
Set objFolder = objShellApp.BrowseForFolder _
    (WINDOW_HANDLE, "Select a folder:", NO_OPTIONS, "C:\Program Files")       
Set objFolderItem = objFolder.Self

objPath = objFolderItem.Path
If objPath <> "" Then
' Write the Users choise to the Installation Path
	InstallPath.Value = objPath & "\Apps2Run"
Else
' If no value is selected revert to Default
	InstallPath.Value = "C:\Program Files\Apps2Run"
End If
End Sub

Public Sub InstallApps2Run()
'########################
' This Sub Installs the Apps2Run Application
'########################
' it creates the apropriate Registry Keys
Progress.InnerHTML = "installing application in : " & InstallPath.Value 
ProgressBar1.Value = ProgressBar1.Value + 10
objShell.RegWrite "HKLM\Software\Apps2Run\InstallPath",InstallPath.Value,"REG_SZ"
objShell.RegWrite "HKLM\Software\Apps2Run\Verssion",VerNum,"REG_SZ"

'Call the WriteFile Sub and write the Script File
Progress.InnerHTML = "Writing File : " & InstallPath.Value & "\AppPaths.vbs"
WriteFile()
' Write the Uninstall File
WriteUnInstall()
ProgressBar1.Value = ProgressBar1.Value + 30
Progress.InnerHTML = "Copying File and Creating Shortcuts"
' Copy the Editable Script to the Installation Folder
oFilePath = Location.PathName 
oFilePath = Replace(oFilePath,"%20"," ")
Set CFile = objFSO.GetFile(oFilePath)
CFile.Copy(InstallPath.Value & "\Apps2Run.hta")
CreateShortCuts(InstallPath.Value)
Progress.InnerHTML = "Configuring Program..."
ProgressBar1.Value = ProgressBar1.Value + 30
' Write to the Chosen Registry Paths the Shortcut to the Apps2Run File so they could be called from the Run Line
If BATFile.Checked = True Then
	objShell.RegWrite "HKCR\batFile\Shell\Apps2Run\Command\", _
		"Wscript.exe " & chr(34) & InstallPath.Value & chr(34) & "\AppPaths.vbs " & chr(34) & "%1" & chr(34)
	objShell.RegWrite "HKCR\batFile\Shell\Apps2Run\", "Add Application to &Run"
End If
If EXEFile.Checked = True Then
	objShell.RegWrite "HKCR\exeFile\Shell\Apps2Run\Command\", _
		"Wscript.exe " & chr(34) & InstallPath.Value & chr(34) & "\AppPaths.vbs " & chr(34) & "%1" & chr(34)
	objShell.RegWrite "HKCR\exeFile\Shell\Apps2Run\", "Add Application to &Run"
End If
If MSCFile.Checked = True Then
	objShell.RegWrite "HKCR\mscFile\Shell\Apps2Run\Command\", _
		"Wscript.exe " & chr(34) & InstallPath.Value & chr(34) & "\AppPaths.vbs " & chr(34) & "%1" & chr(34)
	objShell.RegWrite "HKCR\mscFile\Shell\Apps2Run\", "Add Application to &Run"
End If
If HTAFile.Checked = True Then
	objShell.RegWrite "HKCR\HTAFile\Shell\Apps2Run\Command\", _
		"Wscript.exe " & chr(34) & InstallPath.Value & chr(34) & "\AppPaths.vbs " & chr(34) & "%1" & chr(34)
	objShell.RegWrite "HKCR\htaFile\Shell\Apps2Run\", "Add Application to &Run"
End If
If VBSFile.Checked = True Then
	objShell.RegWrite "HKCR\vbsFile\Shell\Apps2Run\Command\", _
		"Wscript.exe " & chr(34) & InstallPath.Value & chr(34) & "\AppPaths.vbs " & chr(34) & "%1" & chr(34)
	objShell.RegWrite "HKCR\vbsFile\Shell\Apps2Run\", "Add Application to &Run"
End If
If Folder.Checked = True Then
	objShell.RegWrite "HKCR\Folder\Shell\Apps2Run\Command\", _
		"Wscript.exe " & chr(34) & InstallPath.Value & chr(34) & "\AppPaths.vbs " & chr(34) & "%1" & chr(34)
	objShell.RegWrite "HKCR\Folder\Shell\Apps2Run\", "Open Folder From Run"
End If
ProgressBar1.Value = ProgressBar1.Value + 30
Progress.InnerHTML = "All Done !"
MSGBox "Finish !"
QuitApp
End Sub

Sub ShowEdit()
'########################
' If the Apps2Run is allready installed
' Let the user Edit his Aliases - Remove them and View them (thats it for this Version)
'########################

Dim strComputer,I,J
Dim oReg,strKeyPath,arrValueNames,arrValueTypes,strValue, strPath
Dim RegKey,Aliases,AppKey

strComputer = "."

Set oReg=GetObject("winmgmts:{impersonationLevel=impersonate}!\\" & strComputer & "\root\default:StdRegProv")
strKeyPath = "SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths"
oReg.EnumKey HKEY_LOCAL_MACHINE, strKeyPath, arrValueNames
J = 0
For i=0 To UBound(arrValueNames)
	oReg.GetStringValue HKEY_LOCAL_MACHINE,strKeyPath & "\" & arrValueNames(i),"Apps2Run",strValue
	AppKey = strValue
	RegKey = arrValueNames(i)
	oReg.GetStringValue HKEY_LOCAL_MACHINE,strKeyPath & "\" & arrValueNames(i),"",strPath
	If(strPath <> "") Then
    arrPath = Split(strPath,"\")
    strPath = arrPath(Ubound(arrPath))
    if instr(strPath,".lnk") Then
      strPath = Mid(strPath,1,len(strPath)-4) & " Folder"  
    End If
  End If
' Create a Checkbox for each Aps2Run Alias 
	If AppKey = 1 Then
		Aliases = Aliases & "<INPUT TYPE=" & chr(34) & "checkbox" & chr(34) & " ID=" & chr(34) & "App" & i & chr(34) & " Name=" & RegKey & ">" & ReGKey & " (" & strPath & ")</Input></BR>"
		J = J + 1
	End If
Next
AppCount = i
AppAlias.InnerHTML = Aliases

End Sub

Sub RemoveApps
'########################
' This Sub Removes all the Selected Aliases from the Apps2Run Applictaion
'########################

Dim strComputer
Dim oReg,strKeyPath,arrValueNames,arrValueTypes,strValue
Dim obj,j,ColApps

strComputer = "."
 
Set oReg=GetObject("winmgmts:{impersonationLevel=impersonate}!\\" & strComputer & "\root\default:StdRegProv")
strKeyPath = "SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths"
Set ColApps = document.GetElementsByTagName("input")

For Each obj in ColApps
	If (obj.Type = "checkbox") and (instr(obj.ID,"App") > 0) Then
		if obj.Checked = True Then
			oReg.DeleteKey HKEY_LOCAL_MACHINE, strKeyPath & "\" & obj.Name
			MSGBox obj.Name & " is Removed"
		End If
	End If
Next
End Sub

Sub Window_onLoad()
'########################
' On load Resize the Window and Call the Splash Screen
'########################
    Window.offscreenBuffering = True

    Const ForReading = 1
    strContents = ""
    Set objFSO = CreateObject("Scripting.FileSystemObject")
    If objFso.FileExists("HTA.ini") Then
    	Set objFile = objFSO.OpenTextFile("HTA.ini", ForReading)
    	strContents = objFile.ReadAll
    	objFile.Close
    Else
    	Window_onBeforeUnLoad
    End If
    If Not (strContents = "") Then
      arrContents = Split(strContents, ",")
      window.moveTo arrContents(0), arrContents(1)
    End If
    Window.ResizeTo 600,400
    iTimerID = Window.setInterval("ShowSplash",4000)
    ProgressBar1.Value = 0
End Sub

Sub ShowSplash
'########################
' Set The Splash Screen
'########################
On Error Resume Next
' Check if the Apps2Run Application is Installed by Checking the Registry
INST = objShell.RegRead("HKLM\Software\Apps2Run\InstallPath")
Ver = objShell.RegRead("HKLM\Software\Apps2Run\Verssion")
Splash.Style.Display = "None"
' If the Version Number isn't The Same so there must be an Update
' Show the Main Installation Pane and Exit Sub
If (Ver <> VerNum) Then
		EditSCR.Style.Display = "None"
		Main.Style.Display = "Inline"
		Exit Sub
End If
' Else Check if the Apps2Run is Installed
' And show the Edit Pane
If objFSO.FileExists(INST & "\AppPaths.vbs") Then
		EditSCR.Style.Display = "Inline"
		ShowEdit()
		Main.Style.Display = "None"
Else
' For Every other option show the Main Installation Pane
		EditSCR.Style.Display = "None"
		Main.Style.Display = "Inline"
End If
End Sub

Sub WriteUnInstall()
'########################
' This Sub Writes the UnInstall File to the InstallationFolder
' The UnInstallation File itself Removes all the Aliases Made By Apps2Run
' And Removes every File and Registry Entry the Application has Made
'########################
If objFSO.FolderExists(InstallPath.Value) Then
    Set objFolder = objFSO.GetFolder(InstallPath.Value)
Else
	Set objFolder = objFSO.CreateFolder(InstallPath.Value)
End If
Set objFile = objFSO.CreateTextFile(InstallPath.Value & "\UnInstall-Apps2Run.vbs")
objFile.WriteLine "On Error Resume Next"
objFile.WriteLine "Const HKEY_LOCAL_MACHINE = &H80000002"
objFile.WriteLine "Const ALL_USERS_PROGRAMS = &H17&"
objFile.WriteLine "Const AppsPath = " & chr(34) & "HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths" & chr(34)
objFile.WriteLine "Const InstallPath = " & chr(34) & "HKLM\SOFTWARE\Apps2Run" & chr(34)
objFile.WriteLine ""
objFile.WriteLine "Set objShellApp = CreateObject(" & chr(34) & "Shell.Application" & chr(34) & ")"
objFile.WriteLine "Set objShell = CreateObject(" & chr(34) & "WScript.Shell" & chr(34) & ")"
objFile.WriteLine "Set objFSO = CreateObject(" & chr(34) & "Scripting.FileSystemObject" & chr(34) & ")"
objFile.WriteLine ""
objFile.WriteLine "Dim strComputer,I"
objFile.WriteLine "Dim oReg,strKeyPath,arrValueNames,arrValueTypes,strValue"
objFile.WriteLine "Dim RegKey,Aliases,AppKey"
objFile.WriteLine "strComputer = " & chr(34) & "." & chr(34)
objFile.WriteLine ""
objFile.WriteLine "Set AllUsersFolder = objShellApp.Namespace(ALL_USERS_PROGRAMS)"
objFile.WriteLine "Set AllUsersFolderItem = AllUsersFolder.Self"
objFile.WriteLine "Set oReg=GetObject(" & chr(34) & "winmgmts:{impersonationLevel=impersonate}!\\" & chr(34) & " & strComputer & " & chr(34) & "\root\default:StdRegProv" & chr(34) & ")"
objFile.WriteLine ""
objFile.WriteLine "strKeyPath = " & chr(34) & "SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths" & chr(34) 
objFile.WriteLine "oReg.EnumKey HKEY_LOCAL_MACHINE, strKeyPath, arrValueNames"
objFile.WriteLine "For i=0 To UBound(arrValueNames)"
objFile.WriteLine "		oReg.GetStringValue HKEY_LOCAL_MACHINE,strKeyPath & " & chr(34) & "\" & chr(34) & " & arrValueNames(i)," & chr(34) & "Apps2Run" & chr(34) & ",strValue"
objFile.WriteLine "		AppKey = strValue"
objFile.WriteLine "		RegKey = arrValueNames(i)"
objFile.WriteLine "		If AppKey = 1 Then"
objFile.WriteLine "			oReg.DeleteKey HKEY_LOCAL_MACHINE,strKeyPath & " & chr(34) & "\" & chr(34) & " & arrValueNames(i)"
objFile.WriteLine "	 	End If"
objFile.WriteLine "Next"
objFile.WriteLine ""
objFile.WriteLine "objShell.RegDelete(" & chr(34) & "HKLM\Software\Apps2Run\" & chr(34) & ")"
objFile.WriteLine "objFSO.DeleteFolder AllUsersFolderItem.Path & " & chr(34) & "\Apps2Run" & chr(34)
objFile.WriteLine "objFSO.DeleteFolder " & chr(34) & InstallPath.Value & chr(34) 
objFile.WriteLine "MsgBox " & chr(34) & "Uninstallation of Apps2Run is now Complete" & chr(34) & ",vbOKOnly+vbInformation," & chr(34) & "Apps2Run" & chr(34)

End Sub

Sub WriteFile()
'########################
' This Sub Writes the Installation File to the Installation Folder
' The Installation File Itself contains Registry Entreis to Open Applications From the Run Line
'########################

If objFSO.FolderExists(InstallPath.Value) Then
    Set objFolder = objFSO.GetFolder(InstallPath.Value)
Else
	Set objFolder = objFSO.CreateFolder(InstallPath.Value)
End If
Set objFile = objFSO.CreateTextFile(InstallPath.Value & "\AppPaths.vbs")

objFile.WriteLine "On Error Resume Next"
objFile.WriteLine "Const RegPath = " & chr(34) & "HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\App Paths" & chr(34) & ""
objFile.WriteLine "Set objShell = CreateObject(" & chr(34) & "WScript.Shell" & chr(34) & ")"
objFile.WriteLine "Set objFSO = CreateObject(" & chr(34) & "Scripting.FileSystemObject" & chr(34) & ")"
objFile.WriteLine ""
objFile.WriteLine "Dim objFile"
objFile.WriteLine "Dim AppPath,AppText"
objFile.WriteLine "Dim I,Arg,FoldersLinks"
objFile.WriteLine "Dim strSCRPath,SCRPath"
objFile.WriteLine "Dim arrSCRPath"
objFile.WriteLine ""
objFile.WriteLine "strSCRPath = WScript.ScriptFullName"
objFile.WriteLine "arrSCRPath = Split(strSCRPath," & chr(34) & "\" & chr(34) & ")"
objFile.WriteLine ""
objFile.WriteLine "For i =0 To UBound(arrscrPath) -1"
objFile.WriteLine "	SCRPath = SCRPath & arrSCRPath(i) & " & chr(34) & "\" & chr(34) & ""
objFile.WriteLine "Next"
objFile.WriteLine ""
objFile.WriteLine "If WScript.Arguments.Count > 0 Then"
objFile.WriteLine "	For I = 0 to Wscript.Arguments.Count -1"
objFile.WriteLine "		Arg = Arg & WScript.Arguments(i) & " & chr(34) & " " & chr(34) & ""
objFile.WriteLine "	Next"
objFile.WriteLine "	Arg =  Trim(Arg)"
objFile.WriteLine " Fol = 0"
objFile.WriteLine "	If InStr(Arg," & chr(34) & "." & chr(34) & ") Then"
objFile.WriteLine "	' Assume a File"
objFile.WriteLine "		Set objFile = objFSO.GetFile(Arg)"
objFile.WriteLine "		AppPath = objFile.Path"
objFile.WriteLine "	Else"
objFile.WriteLine "	'Assume a Folder"
objFile.WriteLine "		Set objFolder = objFSO.GetFolder(Arg)"
objFile.WriteLine "		tmp = SCRPath & " & chr(34) & "FoldersLinks" & chr(34)
objFile.WriteLine "		If objFSO.FolderExists(tmp) Then"
objFile.WriteLine "			Set FoldersLinks = objFSO.GetFolder(SCRPath & " & chr(34) & "FoldersLinks" & chr(34) & ")"
objFile.WriteLine "		Else"
objFile.WriteLine "			Set FoldersLinks = objFSO.CreateFolder(SCRPath & " & chr(34) & "FoldersLinks" & chr(34) & ")"
objFile.WriteLine "		End If"
objFile.WriteLine "		Set objShortcut = objShell.CreateShortcut(FoldersLinks & " & chr(34) & "\" & chr(34) & " & objFolder.Name & " & chr(34) & ".lnk" & chr(34) & ")"
objFile.WriteLine "		objShortcut.TargetPath = objFolder.Path"
objFile.WriteLine "		objShortcut.Save"
objFile.WriteLine "		AppPath = FoldersLinks & " & chr(34) & "\" & chr(34) & " & objFolder.Name & " & chr(34) & ".lnk" & chr(34) & ""
objFile.WriteLine "	End If"
objFile.WriteLine "	AppText = " & chr(34) & "" & chr(34) & ""
objFile.WriteLine "	Do While AppText= " & chr(34) & "" & chr(34) & ""
objFile.WriteLine "		AppText = InputBox(" & chr(34) & "Enter a Text for the Selected Application" & chr(34) & "," & chr(34) & "Application Adder" & chr(34) & ")"
objFile.WriteLine "		If AppText = " & chr(34) & "" & chr(34) & " Then"
objFile.WriteLine "			Res = MsgBox (" & chr(34) & "There is no Value in the Text Box," & chr(34) & " & vbTab & vbCrLf & " & chr(34) & "You Must Enter a Value" & chr(34) & " & vbTab & vbCrLf & vbCrLf & " & chr(34) & "Do you want to Quit ?" & chr(34) & ",vbYesNo+vbQuestion," & chr(34) & "Apps2Run" & chr(34) & ")"
objFile.WriteLine "			If Res = 6 Then"
objFile.WriteLine "				WScript.Quit"
objFile.WriteLine "			Else"
objFile.WriteLine "				MsgBox " & chr(34) & "You must enter a Value" & chr(34) & " & vbTab,vbOKOnly+vbInformation," & chr(34) & "Apps2Run" & chr(34) & ""
objFile.WriteLine "			End If"
objFile.WriteLine "		End If"
objFile.WriteLine "	Loop"
objFile.WriteLine "End If"
objFile.WriteLine ""
objFile.WriteLine "objShell.RegWrite RegPath & " & chr(34) & "\" & chr(34) & " & AppText & " & chr(34) & ".exe\" & chr(34) & ",AppPath"
objFile.WriteLine "objShell.RegWrite RegPath & " & chr(34) & "\" & chr(34) & " & AppText & " & chr(34) & ".exe\Apps2Run" & chr(34) & ",1"

End Sub

Sub QuitApp
'########################
' This Sub Quits the Apps2Run Application
'########################
	Document.Close
	Window.Close
End Sub

</Script>

<Body bgColor="buttonFace">
<DIV ID="Splash" Style="Height:200;Width:400;Border:0.1 mm solid black;position:Relative;Top:50;Left:75;Font 14pt Calibri;Filter:ProgID:DXImageTransform.Microsoft.Gradient(GradientType=0,StartColorStr='#4169E1',EndColorStr='#F0F8FF')ProgID:DXImageTransform.Microsoft.DropShadow(0FFx=10,0ffy=10,color='Gray',Positive='True')">
<Center>
<BR>
<H1>Application 2 Run</H1>
<H2>V 3.2</H2>
<H3>Coded By Assaf Miron<br>31/12/07</H3>
</Center>
</DIV>
<DIV ID="Main" Style="Display:none;Position:Absolute">
<B>Choose where you want to install the Program :</B></BR>
<INPUT TYPE="text" NAME="InstallPath" Value="C:\Program Files\Apps2Run" style="WIDTH: 300px; HEIGHT: 24px"></INPUT>&nbsp;&nbsp;&nbsp;<INPUT TYPE="button" Value="Browse" onClick="BrowseFolder"></Input>	
<BR>
<P>
<B>Choose which file types you want to add :</B></BR>
<INPUT TYPE="checkbox" NAME="BATFile">Bach Files (*.bat)</Input></BR>
<INPUT TYPE="checkbox" NAME="MSCFile">MMC Files (*.msc)</Input></BR>
<INPUT TYPE="checkbox" NAME="EXEFile">Executible Files (*.exe)</Input></BR>
<INPUT TYPE="checkbox" NAME="HTAFile">HTML Applications Files (*.hta)</Input></BR>
<INPUT TYPE="checkbox" NAME="VBSFile">VBS Files (*.vbs)</Input></BR>
<INPUT TYPE="checkbox" NAME="Folder">Folders</Input></BR>
</P>
<BR>
<INPUT TYPE="button" Value="Next" onClick="InstallApps2Run"></Input>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;<INPUT TYPE="button" Value="Exit" onClick="QuitApp"></Input>
<p><span id=Progress></span></p>
<p>
<object classid='clsid:35053A22-8589-11D1-B16A-00C0F0283628' id='ProgressBar1' height='20' width='400'>
    <param name='Min' value='0'>
    <param name='Max' value='100'>
    <param name='Orientation' value='0'>
    <param name='Scrolling' value='1'>
</object>
</p>

</DIV>
<DIV ID="EditSCR" Style="Display:none;Position:Absolute">
<B><U>Applications Currently in Run</U></B>
<HR>
<P><span id=AppAlias></span></P>
</BR><INPUT TYPE="button" Value="Remove Selected" onClick="RemoveApps"></Input>&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
<INPUT TYPE="button" Value="Exit" onClick="QuitApp"></Input>
</DIV>
</Body>
</HTML>
     