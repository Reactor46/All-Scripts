<html>
<head>

<meta http-equiv="Content-Type" content="text/html; charset=windows-1252">
<meta name="description" content="Created by Stuart Barrett">
<meta name="description" content="Last Update: 23/05/11">

<script language="VBScript">
	intLeft = window.screenLeft
	intTop = window.screenTop
	window.moveTo -2000,-2000
	window.ResizeTo 1,1
</script>

<title>AD Update Utility</title>

<HTA:APPLICATION 
    id="objADUCUpdate" 
	applicationname="ADUCUpdateUtil"	
	border="thin"
	borderstyle="normal"
	caption="ADUC Update Utility"
	contextmenu="no"
	maximizebutton="no"
	minimizebutton="yes"
	navigable="yes"
	scroll="no"
	selection="yes"
	singleinstance="yes"
	sysmenu="yes"
	version="3.5"
	windowstate="normal" />

<style type="text/css">
body {
	font-size: 0.8em;
	cursor: default;
	font-family: "trebuchet ms", calibri, helvetica, sans-serif, "Times New Roman";
	background-color: #eeeeee;
}
input {
	font-family: "trebuchet ms", calibri, helvetica, sans-serif, "Times New Roman";
}
input.button {
	color: black;
	cursor: hand;
	background-color: white;
	border: #000033 2px solid;
	font-weight: bold;
}
input.text {
	border: #000033 1px solid;
	font-weight: bold;
}
input.btnhov { 
	border-color: #000033;
	background-color: #bbbbbb;
}
input.disabled { 
	background-color: #eeeeee;
	border-color: #888888;
	cursor: default;
}
input.userdis {
	border-color: #888888;
	cursor: default;
	border-width: 2px;
}
select {
	font-family: "trebuchet ms", calibri, helvetica, sans-serif, "Times New Roman";
	border: #000033 1px solid;
}
select.disabled { 
	background-color: #eeeeee;
	color: #888888;
	border-color: #888888;
}
.spanlink {
	text-decoration: underline;
	font-weight:bold;
	color: blue;
	cursor: hand;
}
.hidden {
	display: none;
	visibility: hidden;
}
#DNSpan {
	background-color: white;
	font-weight: bold;
	overflow: auto;
	width: 100%;
	height: 50;
	border: #000033 1px solid;
	padding: 5px;
	margin: 5px;
	margin-top: 15px;
}
#DNSpan.disabled {
	background-color: #eeeeee;
	border-color: #888888;
}
#UsernameSearchSpan {
	position: absolute;
	left: 30px;
	bottom: 100px;
	width: 350px;
	height: 350px;
	overflow: auto;
}
#VersionInfo {
	position: absolute;
	top: 20px;
	right: 10px;
	font-style: italic;
	color: #888888;
}
</style>

</head>

<script Language="VBScript">

'#==============================================================================
'#==============================================================================
'#  SCRIPT.........:	ADUpdateUtility.hta
'#  AUTHOR.........:	Stuart Barrett
'#  VERSION........:	3.5
'#  CREATED........:	16/08/10
'#  LICENSE........:	Freeware
'#  REQUIREMENTS...:  
'#
'#  DESCRIPTION....:	Allows updating of any AD user account details
'#
'#  NOTES..........:	
'# 
'#  CUSTOMIZE......:  
'#==============================================================================
'#  REVISED BY.....:  
'#  EMAIL..........:  
'#  REVISION DATE..:  
'#  REVISION NOTES.:
'#
'#==============================================================================
'#==============================================================================

	Dim intFound, strDNSDomain, strDefaultEmail

	'#--------------------------------------------------------------------------
	'#	SUBROUTINE.....:	UserSearch()
	'#	PURPOSE........:	Performs search in AD for specified user and
	'#						displays current info
	'#	ARGUMENTS......:	
	'#	EXAMPLE........:	
	'#	NOTES..........:	
	'#--------------------------------------------------------------------------
	Sub UserSearch()
		Set objShell = CreateObject("WScript.Shell")
		
		If txtUsername.Value = "" OR (txtUsername.Value = "Username" _
		AND txtUsername.style.color = "#888888") Then _
			txtUsername.Value = objShell.ExpandEnvironmentStrings("%USERNAME%")
		strUser = txtUsername.Value

		If strDNSDomain = "" OR IsNull(strDNSDomain) Then
			Set objRootDSE = GetObject("LDAP://RootDSE")
			strDNSDomain = objRootDSE.Get("defaultNamingContext")
		End If
		
		Set objConnection = CreateObject("ADODB.Connection")
		objConnection.Open "Provider=ADsDSOObject;"

		Set objCommand = CreateObject("ADODB.Command")
		objCommand.ActiveConnection = objConnection
		
		Set objCommand.ActiveConnection = objConnection
		strBase = "<LDAP://" & strDNSDomain & ">"

		strFilter = "(sAMAccountName=" & strUser & ")"
		strAttributes = "distinguishedName,proxyAddresses"
		strQuery = strBase & ";" & strFilter & ";" & strAttributes & ";subtree"
		
		objCommand.CommandText = strQuery
		objCommand.Properties("Page Size") = 1000
		objCommand.Properties("Timeout") = 30
		objCommand.Properties("Cache Results") = False
		
		On Error Resume Next
		
		Set objRecordset = objCommand.Execute

		If (objRecordset.EOF = True) Then
			UnknownUserSearch()
			Exit Sub
		End If
		
		Set objOption = Document.createElement("OPTION")
		objOption.Text = "Custom..."
		objOption.Value = 0
		objOption.Title = "Enter a custom email address"
		EmailSelect.Add(objOption)
		
		EmailLabel.InnerHTML = "Custom"
		DelProxyLabel.InnerHTML = "X"
		EmailSelect.className = ""

		Do Until objRecordset.EOF
			strUserDN = objRecordset.Fields("distinguishedName").Value
			arrProxyAddresses = objRecordSet.Fields("proxyAddresses").Value
			Err.Clear
			If IsArray(objRecordSet.Fields("proxyAddresses")) Then
				For Each objItem in arrProxyAddresses
					If Left(LCase(objItem), 5) = "smtp:"  Then
						objItem = Replace(LCase(objItem), "smtp:", "")
	
						Set objOption = Document.createElement("OPTION")
						objOption.Text = objItem
						objOption.Value = objItem
						objOption.Title = objItem
						EmailSelect.Add(objOption)
					End If
				Next
				Else
					ChangeEmailType()	
			End If
			objRecordset.MoveNext
		Loop
		objRecordset.Close

		Set objUser = GetObject("LDAP://" & strUserDN)

		strDispName = objUser.Get("displayName")
		strStreetAddress = objUser.Get("streetAddress")
		strPOBOX = objUser.Get("postOfficeBox")
		strCity = objUser.Get("l")
		strPostcode = objUser.Get("postalCode")
		strTelephoneNumber = objUser.Get("telephoneNumber")
		strMobile = objUser.Get("mobile")
		strFaxNo = objUser.Get("facsimileTelephoneNumber")
		strTitle = objUser.Get("title")
		strOffice = objUser.Get("physicalDeliveryOfficeName")
		strCounty = objUser.Get("st")
		strEmail = objUser.Get("mail")
		strCompany = objUser.Get("company")
		strCountry = objUser.Get("co")
		strDepartment = objUser.Get("department")
		strDNTitle = objUser.Get("distinguishedName")
		strDN = Replace(strDNTitle, ",", ", ")
		strDescription = objUser.Get("description")
		strFirstName = objUser.Get("givenName")
		strSurname = objUser.Get("sn")
		
		For Each objOption In EmailSelect.Options
			If LCase(objOption.Value) = LCase(strEmail) Then 
				objOption.Selected = True
				strDefaultEmail = objOption.Value
			End If
		Next
		
		txtDispName.Value = strDispName
		txtAdd.Value = strStreetAddress
		txtCity.Value = strCity
		txtPostcode.Value = strPostcode
		txtPhN.Value = strTelephoneNumber
		txtMobile.Value = strMobile
		txtTitle.Value = strTitle
		txtCounty.Value = strCounty
		txtEmailAdd.Value = strEmail
		txtFirstName.Value = strFirstName
		txtSurname.Value = strSurname
		txtFaxNo.Value = strFaxNo
		txtCountry.Value = strCountry
		txtDescription.Value = strDescription
		txtOffice.Value = strOffice
		txtCompany.Value = strCompany
		txtDepartment.Value = strDepartment
		DNSpan.InnerHTML = strDN
		DelProxyLabel.InnerHTML = "X"
		
		txtDispName.Title = "Display Name: " & strDispName
		txtAdd.Title = "Street Address: " & strStreetAddress
		txtCity.Title = "City: " & strCity
		txtPostcode.Title = "Postcode: " & strPostcode
		txtPhN.Title = "Telephone: " & strTelephoneNumber
		txtMobile.Title = "Mobile Phone: " & strMobile
		txtTitle.Title = "Title: " & strTitle
		txtCounty.Title = "County: " & strCounty
		txtEmailAdd.Title = "Email Address: " & strEmail
		txtFirstName.Title = "First Name: " & strFirstName
		txtSurname.Title = "Surname: " & strSurname
		txtFaxNo.Title = "Fax Number: " & strFaxNo
		txtCountry.Title = "Country: " & strCountry
		txtDescription.Title = "Description: " & strDescription
		txtOffice.Title = "Office: " & strOffice
		txtCompany.Title = "Company: " & strCompany
		txtDepartment.Title = "Department: " & strDepartment
		EmailSelect.Title = "Select Email Address"
		DNSpan.Title = "DN: " & strDNTitle
		EmailLabel.Title = "Enter a custom email address"
		DelProxyLabel.Title = "Delete selected proxy address"
		btnUpdateInf.Title = "Update account details"
		btnPrintNow.Title = "Print Window"
		btnReset.Title = "Reset Form"
		btnSearch.Title = ""
		txtUsername.Title = "Username: " & strUser
		
		txtAdd.Disabled = False
		txtCity.Disabled = False
		txtPostcode.Disabled = False
		txtPhN.Disabled = False
		txtMobile.Disabled = False
		txtTitle.Disabled = False
		txtCounty.Disabled = False
		txtDispName.Disabled = False
		txtFirstName.Disabled = False
		txtSurname.Disabled = False
		txtFaxNo.Disabled = False
		txtCountry.Disabled = False
		txtDescription.Disabled = False
		txtOffice.Disabled = False
		txtCompany.Disabled = False
		txtDepartment.Disabled = False
		EmailSelect.Disabled = False
		DNSpan.Disabled = False
		btnUpdateInf.Disabled = False
		btnPrintNow.Disabled = False
		btnReset.Disabled = False
		btnSearch.Disabled = True

		txtAdd.className = "text"
		txtCity.className = "text"
		txtPostcode.className = "text"
		txtPhN.className = "text"
		txtMobile.className = "text"
		txtTitle.className = "text"
		txtCounty.className = "text"
		txtDispName.className = "text"
		txtFirstName.className = "text"
		txtSurname.className = "text"
		txtFaxNo.className = "text"
		txtCountry.className = "text"
		txtDescription.className = "text"
		txtOffice.className = "text"
		txtCompany.className = "text"
		txtDepartment.className = "text"
		DNSpan.className = "dnspan"
		btnUpdateInf.className = "button"
		btnPrintNow.className = "button"
		btnReset.className = "button"
		btnSearch.className = "button disabled"
		txtUsername.Value = " " & Trim(txtUsername.Value)
		txtUsername.className = "text userdis"
		txtUsername.Disabled = True
		
		GetOtherInfo()
		
		intFound = 1
		
		txtDispName.Focus()
	End Sub
	
	'#--------------------------------------------------------------------------
    '#	SUBROUTINE.....:	SelectEmailAdd()
    '#	PURPOSE........:	Changes Email input to Custom if required
    '#	ARGUMENTS......:	
    '#	EXAMPLE........:	
    '#	NOTES..........:	
    '#--------------------------------------------------------------------------
	Sub SelectEmailAdd()
		If EmailSelect.Value = "0" Then ChangeEmailType()
	End Sub
	
	'#--------------------------------------------------------------------------
    '#	SUBROUTINE.....:	ChangeEmailType()
    '#	PURPOSE........:	Changes Email input
    '#	ARGUMENTS......:	
    '#	EXAMPLE........:	
    '#	NOTES..........:	
    '#--------------------------------------------------------------------------
	Sub ChangeEmailType()
		If EmailLabel.InnerHTML = "Custom" Then
			txtEmailAdd.Value = strDefaultEmail
			EmailSelect.className = "hidden"
			txtEmailAdd.className = "text"
			EmailLabel.InnerHTML = "Select"
			EmailLabel.Title = "Select an email address from a list of proxy addresses assigned to the user"
			DelProxyLabel.InnerHTML = ""
			DelProxyLabel.Title = ""
			ElseIf EmailLabel.InnerHTML = "Select" Then
				EmailSelect.Value = strDefaultEmail
				EmailSelect.className = ""
				txtEmailAdd.className = "text hidden"
				EmailLabel.InnerHTML = "Custom"
				EmailLabel.Title = "Enter a custom email address"
				DelProxyLabel.InnerHTML = "X"
				DelProxyLabel.Title = "Delete selected proxy address"
		End If
	End Sub
	
	'#--------------------------------------------------------------------------
    '#	SUBROUTINE.....:	DeleteProxyAddress()
    '#	PURPOSE........:	Deletes selected proxy address
    '#	ARGUMENTS......:	
    '#	EXAMPLE........:	
    '#	NOTES..........:	
    '#--------------------------------------------------------------------------
	Sub DeleteProxyAddress()
		Const ADS_PROPERTY_DELETE = 4
		
		strUser = txtUsername.Value
		strEmailDel = EmailSelect.Value
		
		ContinuePrompt = MsgBox("Are you sure you wish to delete the proxy address '" & _
		strEmailDel & "'?", vbQuestion+vbYesNo, "AD Management Utility")
		
		If ContinuePrompt = vbYes Then
			Set objConnection = CreateObject("ADODB.Connection")
			objConnection.Open "Provider=ADsDSOObject;"

			Set objCommand = CreateObject("ADODB.Command")
			objCommand.ActiveConnection = objConnection
			
			Set objCommand.ActiveConnection = objConnection
			strBase = "<LDAP://" & strDNSDomain & ">"

			strFilter = "(sAMAccountName=" & strUser & ")"
			strAttributes = "distinguishedName,proxyAddresses"
			strQuery = strBase & ";" & strFilter & ";" & strAttributes & ";subtree"
			
			objCommand.CommandText = strQuery
			objCommand.Properties("Page Size") = 1000
			objCommand.Properties("Timeout") = 30
			objCommand.Properties("Cache Results") = False
			
			On Error Resume Next
			
			Set objRecordset = objCommand.Execute
			
			If (objRecordset.EOF = True) Then
				MsgBox "The username '" & strUser & "' cannot be found.", vbExclamation, "Error"
				CleanUp()
				Exit Sub
			End If

			Do Until objRecordset.EOF
				strUserDN = objRecordset.Fields("distinguishedName").Value
				arrProxyAddresses = objRecordSet.Fields("proxyAddresses").Value
				objRecordset.MoveNext
			Loop
			objRecordset.Close
			
			Set objUser = GetObject("LDAP://" & strUserDN)
			
			strEmail = objUser.Get("mail")
			
			For Each objItem In arrProxyAddresses
				x = Len(objItem)
				If Right(LCase(objItem), Len(objItem) - 5) = LCase(strEmailDel) Then
					objUser.PutEx ADS_PROPERTY_DELETE, "proxyAddresses", Array(objItem)
					Else
						If LCase(strEmail) = LCase(strEmailDel) Then
							strEmail = Right(objItem, Len(objItem) - 5)
							objUser.Put "mail", strEmail
						End If
				End If
			Next
		
			objUser.SetInfo
			
			If Err.Number = 0 Then
				CleanUp()
				txtUsername.Value = strUser
				UserSearch()
				MsgBox "The proxy address '" & strEmailDel & "' has now been deleted", _
				vbInformation, "AD Update Utility"
				Else
					MsgBox "There was an error removing the proxy address '" & _
					strEmailDel & "'.", vbExclamation, "Error"
					CleanUp()
			End If
			Else
				CleanUp()
				txtUsername.Value = strUser
				UserSearch()
		End If
	End Sub

	'#--------------------------------------------------------------------------
    '#	SUBROUTINE.....:	UpdateInfo()
    '#	PURPOSE........:	Updates user info in AD
    '#	ARGUMENTS......:	
    '#	EXAMPLE........:	
    '#	NOTES..........:	
    '#--------------------------------------------------------------------------
	Sub UpdateInfo()
		If txtDispName.Value <> "" AND txtUsername.Value <> "" _
		AND intFound = 1 Then
			strUser = txtUsername.Value

			Set objConnection = CreateObject("ADODB.Connection")
			objConnection.Open "Provider=ADsDSOObject;"

			Set objCommand = CreateObject("ADODB.Command")
			objCommand.ActiveConnection = objConnection
			
			Set objCommand.ActiveConnection = objConnection
			strBase = "<LDAP://" & strDNSDomain & ">"

			strFilter = "(sAMAccountName=" & strUser & ")"
			strAttributes = "distinguishedName,proxyAddresses"
			strQuery = strBase & ";" & strFilter & ";" & strAttributes & ";subtree"
			
			objCommand.CommandText = strQuery
			objCommand.Properties("Page Size") = 100
			objCommand.Properties("Timeout") = 30
			objCommand.Properties("Cache Results") = False
			
			On Error Resume Next
			
			Set objRecordset = objCommand.Execute
			
			If (objRecordset.EOF = True) Then
				MsgBox "The username '" & strUser & "' cannot be found.", vbExclamation, "Error"
				CleanUp()
				Exit Sub
			End If

			Do Until objRecordset.EOF
				strUserDN = objRecordset.Fields("distinguishedName").Value
				arrProxyAddresses = objRecordSet.Fields("proxyAddresses").Value
				objRecordset.MoveNext
			Loop
			objRecordset.Close

			Set objUser = GetObject("LDAP://" & strUserDN)

			objUser.Put "streetAddress", txtAdd.Value
			objUser.Put "l", txtCity.Value
			objUser.Put "PostalCode", txtPostcode.Value
			objUser.Put "TelephoneNumber", txtPhN.Value
			objUser.Put "mobile", txtMobile.Value
			objUser.Put "title", txtTitle.Value
			objUser.Put "st",txtCounty.Value
			objUser.Put "givenName",txtFirstName.Value
			objUser.Put "sn",txtSurname.Value
			objUser.Put "facsimileTelephoneNumber",txtFaxNo.Value
			objUser.Put "co",txtCountry.Value
			objUser.Put "description",txtDescription.Value
			objUser.Put "physicalDeliveryOfficeName",txtOffice.Value
			objUser.Put "company",txtCompany.Value
			objUser.Put "department",txtDepartment.Value
			
			If EmailLabel.InnerHTML = "Custom" Then
				objUser.Put "mail",EmailSelect.Value
				ElseIf EmailLabel.InnerHTML = "Select" Then
					x = UBound(arrProxyAddresses) + 1
					ReDim Preserve arrProxyAddresses(x)
					arrProxyAddresses(x) = "smtp:" & txtEmailAdd.Value
					objUser.Put "proxyAddresses",arrProxyAddresses
					objUser.Put "mail",txtEmailAdd.Value
			End If
			
			Err.Clear
			
			objUser.SetInfo
			
			If Err.Number <> 0 Then 
				MsgBox "There was an error updating the user details for '" & _
				Trim(txtUsername.Value) & "':  " & vbCrLf & vbCrLf & _
				Err.Number & ": " & Err.Description, vbExclamation, "Error"
				Err.Clear
				Else
					MsgBox "The user details for '" & Trim(txtUsername.Value) & _
					"' have now been updated.", vbInformation, "ADUC Update Utility"
			End If
			
			CleanUp()
			txtUsername.Value = strUser
			UserSearch()
			Else
				CleanUp()
		End If
	End Sub
	
	'#--------------------------------------------------------------------------
    '#	SUBROUTINE.....:	UnknownUserSearch()
    '#	PURPOSE........:	Performs search where unknown username entered,
	'#						shows info in table on left of app
    '#	ARGUMENTS......:	
	'#						
    '#	EXAMPLE........:	
    '#	NOTES..........:	
    '#--------------------------------------------------------------------------
	Sub UnknownUserSearch()
		strInput = txtUsername.Value
		
		strHTML = "<table style=""border-collapse:collapse;table-layout:fixed;"">"
		strHTML = strHTML & "<tr><th colspan=2 style=""background-color:black;color:white;"">"
		strHTML = strHTML & "<span style=""float:left;margin-left:125px;"">Search</span>"
		strHTML = strHTML & "<span style=""float:right;cursor:hand;margin-right:10px;"" onClick=""CloseSearch()"" title=""Close"">X</span></th></tr>"
		
		Set objConnection = CreateObject("ADODB.Connection")
		objConnection.Open "Provider=ADsDSOObject;"
		
		Set objCommand = CreateObject("ADODB.Command")
		objCommand.ActiveConnection = objConnection
		
		Set objCommand.ActiveConnection = objConnection
		strBase = "<LDAP://" & strDNSDomain & ">"
		
		strFilter = "(&(objectClass=user)(|(sAMAccountName=*" & strInput & "*)(displayName=*" & strInput & "*)))"
		strAttributes = "distinguishedName,sAMAccountName,displayName,objectCategory"
		strQuery = strBase & ";" & strFilter & ";" & strAttributes & ";subtree"
		
		objCommand.CommandText = strQuery
		objCommand.Properties("Page Size") = 1000
		objCommand.Properties("Timeout") = 30
		objCommand.Properties("Cache Results") = False
		objCommand.Properties("Sort on") = "sAMAccountName"
		
		On Error Resume Next
		
		Set objRecordset = objCommand.Execute
		
		If objRecordset.EOF = True Then
			CloseSearch()
			MsgBox "No users have been found.", vbInformation, "AD Update Utility"
			Exit Sub
		End If
		
		Do Until objRecordset.EOF
			strUser = objRecordset.Fields("sAMAccountName").Value
			strName = objRecordset.Fields("displayName").Value
			objRecordset.MoveNext
			
			If NOT IsNull(strUser) AND NOT IsNull(strName) Then
				strNewUser = Replace(strUser, "'", "||")
				strNewUser = Replace(strNewUser, Chr(34), "{}")
				strNewUser = Replace(strNewUser, " ", "<>")
				strUser = Replace(strUser, "'", "")
				strUser = Replace(strUser, Chr(34), "")
				strName = Replace(strName, "'", "")
				strName = Replace(strName, Chr(34), "")
				
				strHTML = strHTML & "<tr>"
				strHTML = strHTML & 	"<td style=""background-color:white;border-left:1px solid black;border-bottom:1px solid black;"">"
				strHTML = strHTML & 		"<span class=""spanlink"" onClick='ShowSearchUser """ & strNewUser & """' title=""Pick user '" & strUser & "'"">" & strUser & "</span>"
				strHTML = strHTML & 	"</td>"
				strHTML = strHTML & 	"<td style=""background-color:white;border-right:1px solid black;border-bottom:1px solid black;"">"
				strHTML = strHTML & 		strName
				strHTML = strHTML & 	"</td>"
				strHTML = strHTML & "</tr>"
			End If
		Loop
		
		objRecordset.Close

		strHTML = strHTML & "</table>"
		
		UsernameSearchSpan.InnerHTML = strHTML
		UsernameSearchSpan.className = ""
	End Sub
	
	'#--------------------------------------------------------------------------
    '#	SUBROUTINE.....:	ShowSearchUser(strUser)
    '#	PURPOSE........:	Shows the info for the chosen user
    '#	ARGUMENTS......:	strUser = Username
    '#	EXAMPLE........:	ShowSearchUser("johsmi")
    '#	NOTES..........:	
    '#--------------------------------------------------------------------------
	Sub ShowSearchUser(strUser)
		UsernameSearchSpan.className = "hidden"
		strUser = Replace(strUser, "||", "'")
		strUser = Replace(strUser, "{}", Chr(34))
		strUser = Replace(strUser, "<>", " ")
		txtUsername.Value = strUser
		UserSearch()
	End Sub
	
	'#--------------------------------------------------------------------------
    '#	SUBROUTINE.....:	CloseSearch()
    '#	PURPOSE........:	Closes search box
    '#	ARGUMENTS......:	
    '#	EXAMPLE........:	
    '#	NOTES..........:	
    '#--------------------------------------------------------------------------
	Sub CloseSearch()
		txtUsername.Value = "Username"
		txtUsername.style.color = "#888888"
		UsernameSearchSpan.className = "hidden"
	End Sub

	'#--------------------------------------------------------------------------
    '#	SUBROUTINE.....:	CleanUp()
    '#	PURPOSE........:	Resets form
    '#	ARGUMENTS......:	
    '#	EXAMPLE........:	
    '#	NOTES..........:	
    '#--------------------------------------------------------------------------
	Sub CleanUp()
		txtAdd.Value = ""
		txtCity.Value = ""
		txtPostcode.Value = ""
		txtPhN.Value = ""
		txtMobile.Value = ""
		txtTitle.Value = ""
		txtCounty.Value = ""
		txtDispName.Value = ""
		txtUsername.Value = ""
		txtEmailAdd.Value = ""
		txtFirstName.Value = ""
		txtSurname.Value = ""
		txtFaxNo.Value = ""
		txtCountry.Value = ""
		txtDescription.Value = ""
		txtOffice.Value = ""
		txtCompany.Value = ""
		txtDepartment.Value = ""
		DNSpan.InnerHTML = "&nbsp;"
		EmailLabel.InnerHTML = ""
		DelProxyLabel.InnerHTML = ""
		
		txtDispName.Title = ""
		txtAdd.Title = ""
		txtCity.Title = ""
		txtPostcode.Title = ""
		txtPhN.Title = ""
		txtMobile.Title = ""
		txtTitle.Title = ""
		txtCounty.Title = ""
		txtFirstName.Title = ""
		txtSurname.Title = ""
		txtFaxNo.Title = ""
		txtCountry.Title = ""
		txtDescription.Title = ""
		txtOffice.Title = ""
		txtCompany.Title = ""
		txtDepartment.Title = ""
		EmailSelect.Title = ""
		DNSpan.Title = ""
		EmailLabel.Title = ""
		DelProxyLabel.Title = ""
		btnUpdateInf.Title = ""
		btnPrintNow.Title = ""
		btnReset.Title = ""
		btnSearch.Title = "Search username"
		
		txtAdd.Disabled = True
		txtCity.Disabled = True
		txtPostcode.Disabled = True
		txtPhN.Disabled = True
		txtMobile.Disabled = True
		txtTitle.Disabled = True
		txtCounty.Disabled = True
		txtDispName.Disabled = True
		txtFirstName.Disabled = True
		txtSurname.Disabled = True
		txtFaxNo.Disabled = True
		txtCountry.Disabled = True
		txtDescription.Disabled = True
		txtOffice.Disabled = True
		txtCompany.Disabled = True
		txtDepartment.Disabled = True
		EmailSelect.Disabled = True
		btnUpdateInf.Disabled = True
		btnPrintNow.Disabled = True
		btnReset.Disabled = True
		btnSearch.Disabled = False
		
		ClearListbox(EmailSelect)
		
		txtAdd.className = "text disabled"
		txtCity.className = "text disabled"
		txtPostcode.className = "text disabled"
		txtPhN.className = "text disabled"
		txtMobile.className = "text disabled"
		txtTitle.className = "text disabled"
		txtCounty.className = "text disabled"
		txtDispName.className = "text disabled"
		txtEmailAdd.className = "text hidden"
		txtFirstName.className = "text disabled"
		txtSurname.className = "text disabled"
		txtFaxNo.className = "text disabled"
		txtCountry.className = "text disabled"
		txtDescription.className = "text disabled"
		txtOffice.className = "text disabled"
		txtCompany.className = "text disabled"
		txtDepartment.className = "text disabled"
		EmailSelect.className = "disabled"
		btnUpdateInf.className = "button disabled"
		btnPrintNow.className = "button disabled"
		btnReset.className = "button disabled"
		btnSearch.className = "button"
		DNSpan.className = "dnspan disabled"
		
		strCompany = ""
		strCountry = ""
		strOffice = ""
		strDescription = ""
		strFirstName = ""
		strSurname = ""
		strFaxNo = ""
		
		txtUsername.Disabled = False
		txtUsername.className = "text"
		txtUsername.Value = "Username"
		txtUsername.Title = "Username"
		txtUsername.style.color = "#888888"
		intFound = 0
		
		CloseSearch()
	End Sub
	
'#--------------------------------------------------------------------------
    '#	SUBROUTINE.....:	ClearListbox(objListbox)
    '#	PURPOSE........:	Clears specified listbox
    '#	ARGUMENTS......:	objListbox = name of listbox to clear
    '#	EXAMPLE........:	ClearListbox(EmailSelect)
    '#	NOTES..........:	
    '#--------------------------------------------------------------------------
	Sub ClearListbox(objListbox)
		For Each objOption in objListbox.Options
			objOption.RemoveNode
		Next
	End Sub

	'#--------------------------------------------------------------------------
    '#	SUBROUTINE.....:	ClearUsername()
    '#	PURPOSE........:	Clears the "Username" text onClick
    '#	ARGUMENTS......:	
    '#	EXAMPLE........:	
    '#	NOTES..........:	
    '#--------------------------------------------------------------------------
	Sub ClearUsername()
		If Right(txtUsername.Value, 8) = "Username" Then
			txtUsername.style.color = "black"
			txtUsername.Value = ""
		End If
	End Sub
	
	'#--------------------------------------------------------------------------
    '#	SUBROUTINE.....:	Window_onLoad()
    '#	PURPOSE........:	Performs onLoad actions
    '#	ARGUMENTS......:	
    '#	EXAMPLE........:	
    '#	NOTES..........:	
    '#--------------------------------------------------------------------------
	Sub Window_onLoad()
		self.ResizeTo 800,705
		VersionInfo.InnerHTML = "Version: " & objADUCUpdate.Version
		CleanUp()
		window.moveTo intLeft,intTop
	End Sub

</script>

<body>

<span style="font-weight:bold;font-size:2em;">AD Update Utility</span>
<div id="VersionInfo">
	&nbsp;
</div>
<hr />
 
<table style="width:100%;">
	<tr>
		<td colspan=2 style="height:40px;vertical-align:middle;">
			Display Name:&nbsp;&nbsp;
			<input type="text" name="txtDispName" size="54" disabled=true>
		</td>
	</tr>
	<tr>
		<td style="width:55%;">
			First Name:
		</td>
		<td>
			Surname:
		</td>
	</tr>
	<tr>
		<td>
			<input type="text" name="txtFirstName" size="54" disabled=true>
		</td>
		<td>
			<input type="text" name="txtSurname" size="54" disabled=true>
		</td>
	</tr>
	<tr>
		<td>
			Title:
		</td>
		<td>
			Street Address:
		</td>
	</tr>
	<tr>
		<td>
			<input type="text" name="txtTitle" size="54" disabled=true>
		</td>
		<td>
			<input type="text" name="txtAdd" size="54" disabled=true>
		</td>
	</tr>
	<tr>
		<td>
			Mobile Phone: <span style="color:red;font-size:0.9em;">
			Example: +44 (1234) 567890</span>
		</td>
		<td>
			City:
		</td>
	</tr>
	<tr>
		<td>
			<input type="text" name="txtMobile" size="54" disabled=true>
		</td>
		<td>
			<input type="text" name="txtCity" size="54" disabled=true>
		</td>
	</tr>
	<tr>
		<td>
			Telephone: <span style="color:red;font-size:0.9em;">
			Example: +44 (1234) 567890</span>
		</td>
		<td>
			County:
		</td>
	</tr>
	<tr>
		<td>
			<input type="text" name="txtPhN" size="54" disabled=true>
		</td>
		<td>
			<input type="text" name="txtCounty" size="54" disabled=true>
		</td>
	</tr>
	<tr>
		<td>
			Fax Number: <span style="color:red;font-size:0.9em;">
			Example: +44 (1234) 567890</span>
		</td>
		<td>
			Postcode:
		</td>
	</tr>
	<tr>
		<td>
			<input type="text" name="txtFaxNo" size="54" disabled=true>
		</td>
		<td>
			<input type="text" name="txtPostcode" size="54" disabled=true>
		</td>
	</tr>
	<tr>
		<td>
			Email Address:
		</td>
		<td>
			Country:
		</td>
	</tr>
	<tr>
		<td>
			<select name="EmailSelect" style="width:262;" onChange="SelectEmailAdd()" disabled=true></select>
			<input type="text" name="txtEmailAdd" size="41" style="margin-right:7px;">
			<span id="EmailLabel" class="spanlink" style="text-decoration:none;font-size:0.9em;" onClick="ChangeEmailType()"></span>&nbsp;
			<span id="DelProxyLabel" onClick="DeleteProxyAddress()" style="color:red;font-weight:bold;cursor:hand;font-size:0.9em;"></span>
		</td>
		<td>
			<input type="text" name="txtCountry" size="54" disabled=true>
		</td>
	</tr>
	<tr>
		<td>
			Description:
		</td>
		<td>
			Office:
		</td>
	</tr>
	<tr>
		<td>
			<input type="text" name="txtDescription" size="54" disabled=true>
		</td>
		<td>
			<input type="text" name="txtOffice" size="54" disabled=true>
		</td>
	</tr>
	<tr>
		<td>
			Company:
		</td>
		<td>
			Department:
		</td>
	</tr>
	<tr>
		<td>
			<input type="text" name="txtCompany" size="54" disabled=true>
		</td>
		<td>
			<input type="text" name="txtDepartment" size="54" disabled=true>
		</td>
	</tr>
</table>

<div id="DNSpan">&nbsp;</div>

<br />

<span id="UsernameSearchSpan" onBlur="UsernameSearchSpan.className='hidden'">
	&nbsp;
</span>

<div style="float:left;">
	<input id=runbutton type="button" value="UPDATE" style="width:100;" name="btnUpdateInf" onClick="UpdateInfo()" onMouseOver="btnUpdateInf.className='button btnhov'" onMouseOut="btnUpdateInf.className='button'" disabled=true>
	&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
	<input id=runbutton type="button" value="PRINT" style="width:60;border-right:0px;" name="btnPrintNow" onClick="window.print()" onMouseOver="btnPrintNow.className='button btnhov'" onMouseOut="btnPrintNow.className='button'" disabled=true><input id=runbutton type="button" value="RESET" style="width:60;" name="btnReset"  onClick="CleanUp()" title="Reset form" onMouseOver="btnReset.className='button btnhov'" onMouseOut="btnReset.className='button'" disabled=true>
</div>

<div style="float:right;font-weight:bold;">
	<input type="text" name="txtUsername" size="20" style="border-width:2px;height:27;color:#888888;border-right:0px;" value="Username" onFocus="ClearUsername()" tabindex="1"><input id=runbutton type="button" value="SEARCH" style="width:60;" name="btnSearch" onClick="UserSearch()" onMouseOver="btnSearch.className='button btnhov'" onMouseOut="btnSearch.className='button'" tabindex="2">
</div>

</body>

</html>