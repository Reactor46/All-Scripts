<HTML>
<HEAD>
   <TITLE>Install Printers Script</TITLE>
         <HTA:APPLICATION
         ID              = "oHTA"
         APPLICATIONNAME = "Install Printers Script"
         BORDER          = "Thin"
         CAPTION         = "yes"
         ICON            = "C:\Program Files\Microsoft Office\OFFICE11\REFBAR.ICO"
         SHOWINTASKBAR   = "yes"
         SINGLEINSTANCE  = "yes"
         SYSMENU         = "yes"
         WINDOWSTATE     = "maximize"
         SCROLL          = "yes"
         SCROLLFLAT      = "yes"
         VERSION         = "1.0">
<SCRIPT LANGUAGE="VBScript">
'=================================================================
'LANG		:VBScript
'NAME		:Dave Gest
'Date		:Jan. 17, 2006
'VERSION    : 2
'Description	:Installs printer on print server, shares out the printer.
'				For more information open the HTA and clivk the help link.
'				Script requires the prnadmin.dll be registerd with the system.
'==================================================================
Dim RegPath, Loc, ShareName, dir, PRNDriver, IP, Shell, FSO, Response
Dim kErrorSuccess, KErrorFailure, Net, oPort, oMaster, oPortType, oPrinter, iResult, addPort, Addprinter, Name, Addcomments 
Set Shell = CreateObject("Wscript.Shell")
Set FSO = CreateObject("Scripting.FileSystemObject")
Set Net = CreateObject("Wscript.Network")
Sub Window_Onload
On Error Resume Next
	dir = Shell.ExpandEnvironmentStrings("%Systemroot%\system32\prnadmin.dll")
	dir2 = Shell.ExpandEnvironmentStrings("%Systemroot%\prnadmin.dll")
    dir3 = Shell.ExpandEnvironmentStrings("%programfiles%\Windows Resource Kits\Tools\prnadmin.dll")
	  If FSO.FileExists(Dir) Or FSO.FileExists(Dir2) Or FSO.FileExists(Dir3)Then
        Set oPort = CreateObject("Port.Port.1")
        Set oMaster = CreateObject("PrintMaster.PrintMaster.1")
        set oPrinter = CreateObject("Printer.Printer.1")
        If Err Then ShowFile
        Set SWBemlocator = CreateObject("WbemScripting.SWbemLocator")
		Set objWMIService = SWBemlocator.ConnectServer(net.ComputerName,"root\CIMV2")
		Set colItems = objWMIService.ExecQuery("Select * from Win32_PrinterDriver",,48)
		For Each objItem in colItems
    	         Set objOption = Document.createElement("OPTION")
                objOption.Text = Split(objItem.Name,",")(0)
                objOption.Value = Split(objItem.Name,",")(0)
                WinDrivers.Add(objOption)
	        Next
	  Else
	    ShowFile
	  End If

End Sub

Sub restart
window.alert("Please restart Install_Printer.hta")
window.close
End Sub

Sub OKButton_OnClick
'On Error Resume Next
	   OKClicked.Value = 1
    	kErrorSuccess = 0
    	KErrorFailure = 1
    	Name = Net.ComputerName
    	ShareName = Document.All.ShareName.Value
    	PRNDriver = Document.All.WinDrivers.Value
    	IP = Document.All.IP.Value
    	Loc = Document.All.Loc.Value
    	Addcomments = Document.All.Addcomments.Value
        	If IP = "" Then 
        		Document.All.IP.Value = "Enter IP address!"
        		Document.All.IP.focus()
        		Exit Sub
        	End If
        	If ShareName = "" Then 
        		Document.All.ShareName.Value = "Enter Share Name!"
        		Document.All.ShareName.focus()
        		Exit Sub
        	End If 
        	If Loc = "" Then 
        		Document.All.Loc.Value = "Blank"
        		Document.All.Loc.focus()
        		Exit Sub
        	End If 
	Set ProgressWindow = window.Open("about:blank","Progress_Window","height=300,width=400,left=50,top=50,status=no,titlebar=no,SYSMENU=yes,toolbar=no,menubar=no,location=no,scrollbars=yes") 
	ProgressWindow.Focus()
	ProgressWindow.document.body.style.fontFamily = "Tahoma"
	ProgressWindow.document.body.style.fontSize = "10pt"
	ProgressWindow.document.title = "Create Shared printer progress"
	ProgressWindow.document.body.style.borderStyle = "none"
	ProgressWindow.document.body.style.marginTop = 15
    	'Add Port
    	ProgressWindow.document.body.insertAdjacentHTML "beforeEnd","Please wait while " & oPrinter.Printername & "printer is being installed<br>"
    	oPort.PortName="IP_" & IP & ":" & ShareName
    	oPortType="Raw"
    	oPort.SNMP=False
    	oPort.HostAddress=IP
    	oPort.PortNumber="9100"
    	oMaster.PortAdd oPort
    		if Err.Number = kErrorSuccess Then
    		    ProgressWindow.document.body.insertAdjacentHTML "beforeEnd","Success: Adding port """ & oPort.PortName&"<br>"
    		   iResult = kErrorSuccess
    		Else
    		    ProgressWindow.document.body.insertAdjacentHTML "beforeEnd","Error: Adding port """ & oPort.PortName & """, error: 0x" _
    		                                                                & Hex(Err.Number) & ". " & Err.Description&"<br>"
    	        iResult = KErrorFailure
    	        Err.Clear
    	        Exit Sub
    	   End If
    	AddPort = iResult
    	'Add Printer
    	ProgressWindow.document.body.insertAdjacentHTML "beforeEnd","Setting Printer Properties On " & ShareName & "<br>"
    	oPrinter.ServerName  = "\\" & Name
    	oPrinter.PrinterName = ShareName
    	oPrinter.DriverName = PRNDriver
    	oPrinter.PortName = "IP_" & IP & ":" & ShareName
    	oPrinter.ShareName = ShareName
    	oPrinter.EnableBidi = False
    	oMaster.PrinterAdd oPrinter
        	if Err.Number = kErrorSuccess Then
        	        ProgressWindow.document.body.insertAdjacentHTML "beforeEnd","Success: Adding Printer " & oPrinter.Printername & "<br>"
        		        iResult = kErrorSuccess
        	Else
        		    ProgressWindow.document.body.insertAdjacentHTML "beforeEnd","Error: Adding Printer """ & oPrinter.Printername & """, error: 0x" _
        	                                                        & Hex(Err.Number) & ". " & Err.Description&"<br>"
        	        ProgressWindow.document.body.insertAdjacentHTML "beforeEnd","Please Delete Port """ & oPort.PortName & " before tring to run the script again<br>"
        	        iResult = kErrorFailure
        			Shell.Popup "Please Delete Port """ & oPort.PortName & """ ",5
        			Exit Sub
        	end If
        ProgressWindow.document.body.insertAdjacentHTML "beforeEnd","Finishing final settings on " & oPrinter.Printername & "<br>"
   	    AddPrinter = iResult
    	oPrinter.ServerName  = "\\" & Name
    	oPrinter.PrinterName = ShareName
    	oPrinter.Location = Loc
    	oPrinter.Comment = IP & ":" & ShareName & " " & Addcomments
    	oPrinter.Queued = True
    	oPrinter.Shared = True
    	oPrinter.Published = True
    	oMaster.PrinterSet oPrinter
    	ProgressWindow.document.body.insertAdjacentHTML "beforeEnd","Script Completed<br>"
window.close
End Sub
	
Sub SourceBrowse
On Error Resume Next
Dim getfilename
Set objDialog = CreateObject("UserAccounts.CommonDialog")
objDialog.Filter = "All Files|*.dll"
objDialog.InitialDir = Shell.CurrentDirectory
intResult = objDialog.ShowOpen
 	If intResult = 0 Then
		MsgBox "Please Select a file"
		Exit Sub
	Else
	     FileName.Value = objDialog.FileName
	     FSO.CopyFile objDialog.FileName, Dir
	     Shell.Run "REGSVR32 prnadmin.dll",8,True
	     restart
	End If
Set objDialog = Nothing

End Sub

Sub Continuebutton
'On Error Resume Next
 	If FileName.Value = "" Then
		MsgBox "Please enter a file path and name"
		Exit Sub
	Else
	     FSO.CopyFile FileName.Value, Dir
	     Shell.Run "REGSVR32 prnadmin.dll",8,True
	     restart
	End if

End Sub

Sub ShowFile
On Error Resume Next
Me.focus
OKButton.Disabled = True
windrivers.Disabled = True
DataArea.innerhtml = "Enter name and path of prnadmin.dll file or click Browse<input name='FileName' size='20'><br>"&_
					"<input id=browsebutton  class='button' type='button' value='Browse' name='Browse' onClick='SourceBrowse'><input id=sourcefile  class='button' type='button' value='Continue' name='sourcefile' onClick='Continuebutton'></p>"
					
End Sub

Sub Helpdoc
Dim HelpWindow
	Set helpmeWindow = window.Open("about:blank","Help_Window","height=600,width=800,left=50,top=50,status=no,titlebar=no,SYSMENU=yes,toolbar=no,menubar=no,location=no,scrollbars=yes") 
	helpmeWindow.Focus()
	helpmeWindow.document.body.style.fontFamily = "Tahoma"
	helpmeWindow.document.body.style.fontSize = "10pt"
	helpmeWindow.document.title = "Install Printers Script"
	helpmeWindow.document.body.style.borderStyle = "none"
	helpmeWindow.document.body.style.marginTop = 15
	helpmeWindow.document.body.innerhtml = "The script will check to see if the prnadmin.dll is present in three locations <p>"&_
										   "'C:\Program Files\Windows Resource Kits\Tools', '%Systemroot%, and %Systemroot%\system32'<p>"&_
										   "if found the script will attempt to continue by making a reference to the objects. <p>"&_
										   "If an error occurs from the reference then it redirects to the 'ShowFile' sub routine to get the<p>" &_
										   "prnadmin.dll and copy it to the system32 directory and register it with the system.<p>"&_
										   "The HTA relies on the prnadmin.dll file to create the printer port, share the printer, publish it <p>"&_
										   "to Active Directory, and set the name and comments of the printer properties. <p>"&_
										   "Once the HTA is started you will see four textbox's, a dropdown list and one button.<p>"  &_
										   "All that is needed is the share name you wish to give the printer, the IP address of the printer and to select <p>"&_
										   "the appropriate printer driver from the dropdown list. If the driver is not in the list then you must install the <p>"&_
										   "driver and run the HTA again to have the driver show up in the dropdown. The other two textbox's are optional, as <p>"&_
										   "they refer to the comment and location of the printer's properties and are not required to create the shared printer.<p>"&_
										   "The drop-down box is populated using the WMI and the Win32_PrinterDriver to enumerate all the print drivers installed <p>"&_
										   "on the server. Although you can use the Prnadmin.dll to enumerate printer drivers, I've had some issues consistently <p>"&_
										   "enumerating them from computer to computer in an HTA. In the HTA the user fills in the name of the printer to be <p>"&_
										   "installed, the IP address of the printer, then selects a printer driver from the drop-down list and makes any comments<p>"&_
										   "he wants to identify the printer. When the 'Create Shared Printer' at the bottom of the page is clicked, the script <p>"&_
										   "creates a Standard TCP/IP port with the IP address the user provided and adds the printer's IP address and name to the <p>"&_
										   "ports name to make it easy to find later. Then the script creates the printer and sets properties like disabling <p>"&_
										   "Bidirectional communication, disabling SNMP, and setting the port to 'RAW' and the port number to '9100'. <p>"&_
										   "Step by step progress is reported by the creation of another web page. I use this type of progress<p>"&_
										   "a lot because the starting HTA won't show progress of a script while in execution as it is in a busy state, but having <p>"&_
										   "it create a new window allows the HTA to post updates to the new window while the script is executing. The only <p>"&_
										   "liabilities are that the script is dependent on the prnadmin.dll being registered with the system and that any printer <p>"&_
										   "you install with the script must have its driver installed on the system already. There is a operating system limitation<p>"&_
										   "if you are using an OS older that XP or server 2003 and that is the 'Browse' button functionality if the prnadmin.dll <p>"&_
										   "file is not found. That function is only available with the XP and server 2003. Although the HTA provides a work around <p>"&_
										   "for older OS's and that is the textbox and 'Continue' button. You can get the Windows Server Resource Kit here <p>"&_
										   "http://www.microsoft.com/downloads/details.aspx?FamilyID=9d467a69-57ff-4ae7-96ee-b18c4790cffd&displaylang=en<p>"

	                                       
End Sub
   </SCRIPT>
<BODY>
<h2 align="Center">Install Printers Script</h2>
 <br>This script will install a local printer on this server and share the printer out and set 
printer and printer port properties</br>
<b>NOTE: Printer Driver MUST already be installed for each printer you install with this script!!!!!!!!!!!!!!!!!</b>
  <br>Please fill out the form and click &quot;Create Shared Printer&quot; to create the printer.</br>
<p>
Enter Printer Share Name 
  <input name="ShareName" size="80" maxlength="50">
 <br> IE:&nbsp; Dept_Printer</br>
<p>
Enter IP Address of Printer
 <input name="IP" size="15" maxlength="15">  IE: 192.168.0.250
<p>
 Select a printer driver from the drop down list.
	<select size="1" name="WinDrivers"></select></p>
<p>
  Below is the comment box of the printer properties and will automatically have the IP address 
	and name appended to the line</p>
<p>
 Optional comments that will be added to the comment field <input name="Addcomments" size="36"></p>
<p>
 Optional Printer Location <input name="Loc" size="36"><br></br>
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;
 <input type="button" name="OKButton" value="Create Shared Printer" >
 <input type="hidden" name="OKClicked" size="20">
 <a id="helpme" name="helpme" onclick="Helpdoc" href="#helpme">Help</a></p>
 <span id=DataArea></span>
<body bgcolor="#6699FF" >
</HTML>